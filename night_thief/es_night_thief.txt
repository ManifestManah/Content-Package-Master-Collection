////////////////////////////////
//Various Authors V2 Race Pack//
//Version 1.0.0D              //
////////////////////////////////

block leech_cash
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_xrand wcs_money 25 400
				es playerget cash wcs_tmp1 event_var(userid)
				if (server_var(wcs_tmp1) < server_var(wcs_money)) do
				{
					es_tell event_var(userid) #multi #green [WCS]: #lightgreen Thief - Failed, the target doesn't have enough cash
				}
				if (server_var(wcs_tmp1) >= server_var(wcs_money)) do
				{
					es wcs_setfx cash event_var(userid) - server_var(wcs_money) 0
					es wcs_setfx cash event_var(attacker) + server_var(wcs_money) 0
					es_tell event_var(attacker) #multi #green [WCS]: #lightgreen Thief - You stole server_var(wcs_money) from event_var(es_username)
					es_tell event_var(userid) #multi #green [WCS]: #lightgreen Thief - A rogue stole server_var(wcs_money)$ from you
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 1 (END)
	}
}

block shake
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcs_shake event_var(userid) 5 11 3
				es wcs_centertell event_var(userid) event_var(es_attackername) struck you with his rubble!
				es wcs_centertell event_var(attacker) Rubble caused event_var(es_username) to shake!
				es_playsound event_var(attacker) wcs/firecast.mp3 .6
				es_playsound event_var(userid) wcs/firecast.mp3 .8
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z2 + 5
				es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
				es est_effect_08 #a 0 sprites/fire.vmt server_var(vector2) 275 75 1 1 12 10 1.5 55 220 55 220 2 1
				es est_effect_08 #a 0 sprites/fire.vmt server_var(vector2) 174 175 1 1 12 10 2 55 220 55 220 10 1
				es est_effect_08 #a 0 sprites/fire.vmt server_var(vector2) 174 175 1 1 12 10 2 210 210 10 220 8 1
				es est_effect_08 #a 0 sprites/fire.vmt server_var(vector2) 75 275 1 1 12 10 1.5 55 220 55 220 2 1
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 2 (END)
	}
}

block invisibility
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 3 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		es_delayed .5 wcs_color event_var(userid) 255 255 255 server_var(wcs_invis) 0
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen Cloak of the night - You've become less visible
		es_playsound event_var(userid) wcs/invisibility.mp3 .8
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 340 425 3 35 10 0 200 200 255 220 2
		es_xmath wcs_z1 + 15
		es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 640 650 3 60 10 0 200 200 255 220 2
		es est_effect 11 #a 0 effects/blueflare1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 3 2 180
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 3 (END)
	}
}

block theft
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 4 (START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es wcs_randplayer wcs_targetid server_var(wcs_targetteam)
			if (server_var(wcs_targetid) != 0) do
			{
				es_xdoblock wcs/tools/manifest/night_thief/ultimate_checks
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, there are no living enemies!
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 4 (END)
	}
}

block ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 4 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
				if (server_var(wcs_tmp) != 1) do
				{
					es_xset wcs_ultinotexec 0
					es_xdoblock wcs/tools/manifest/night_thief/ultimate_final
				}
				if (server_var(wcs_tmp) == 1) do
				{
					es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, the targeted enemy was immune!
					es_xset wcs_ultinotexec 1
				}
			}
		}	
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 4 (CHECKS END)
	}
}

block ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 4 (EXECUTED START)
	}
	es playerget health wcs_tmp1 server_var(wcs_userid)
	if (server_var(wcs_tmp1) >= 225) do
	{
		es wcs_setfx health server_var(wcs_userid) = 225 0
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Valuable theft - Your health has reached the cap
	}
	if (server_var(wcs_tmp1) < 225) do
	{
		es wcs_setfx health server_var(wcs_userid) + 25 0
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Valuable theft - Granted you 25 extra health
	}
	es playerget armor wcs_tmp2 server_var(wcs_userid)
	if (server_var(wcs_tmp2) >= 250) do
	{
		es wcs_setfx armor server_var(wcs_userid) = 250 0
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Valuable theft - Your armor has reached the cap
	}
	if (server_var(wcs_tmp2) < 225) do
	{
		es wcs_setfx armor server_var(wcs_userid) + 25 0
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Valuable theft - Granted you 25 extra armour
	}
	es damage server_var(wcs_targetid) 25 1 server_var(wcs_userid)
	es wcs_centertell server_var(wcs_userid) You used valuable theft
	es wcs_centertell server_var(wcs_targetid) You were victim to valuable theft
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 70 1 12 10 .8 0 50 200 200 1
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 70 1 12 10 .8 0 50 200 200 1
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Night Thief - [Block] Skill: 4 (EXECUTED END)
	}
}