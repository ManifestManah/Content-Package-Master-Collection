////////////////////////////////
//Various Authors V3 Race Pack//
//Version 1.0.0D              //
////////////////////////////////

block spawn
{
	es wcsgroup set evasion event_var(userid) 0
	es wcsgroup set evasion_chance event_var(userid) 0
}

block onchange
{
	es wcsgroup set evasion server_var(wcs_userid) 0
	es wcsgroup set evasion_chance server_var(wcs_userid) 0
}

block invisibility
{
	if (event_var(es_userdead) = 0) do
	{
		es_delayed .5 wcs_color event_var(userid) 255 255 255 server_var(wcs_invis) 0
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen Wind walk - You've become less visible
		es_playsound event_var(userid) wcs/invisibility.mp3 .8
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 50 1 30 10 0.8 255 100 100 255 1
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 50 2 60 10 0.8 0 20 100 255 1
		es_xmath wcs_z1 + 50
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 50 3 10 10 0 0 20 25 255 0
	}
}

block disarm
{
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
				es playerget viewcoord server_var(wcs_userid) wcs_x2 wcs_y2 wcs_z2
				es wcs_projectile #a 0 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) effects/blueflare1.vmt 0.1 2 200 16 0.01
				es_xrand wcs_rand 1 3
				if (server_var(wcs_rand) == 1) do
				{
					es wcs_drop event_var(userid) 2
					es_tell event_var(userid) #multi #green [WCS]: #lightgreen Disarm - You were forced to drop your secondary weapon
					es_tell event_var(attacker) #multi #green [WCS]: #lightgreen Disarm - You forced your target to drop his secondary weapon
				}
				if (server_var(wcs_rand) == 2) do
				{
					es wcs_drop event_var(userid) 1
					es_tell event_var(userid) #multi #green [WCS]: #lightgreen Disarm - You were forced to drop your primary weapon
					es_tell event_var(attacker) #multi #green [WCS]: #lightgreen Disarm - You forced your target to drop his primary weapon
				}
				if (server_var(wcs_rand) == 3) do
				{
					es wcs_drop event_var(userid) 1
					es wcs_drop event_var(userid) 2
					es_tell event_var(userid) #multi #green [WCS]: #lightgreen Disarm - You were forced to drop both of your weapons
					es_tell event_var(attacker) #multi #green [WCS]: #lightgreen Disarm - You forced your target to drop both of his weapons
				}
			}
		}
	}
}

block gravity_evasion
{
	if (event_var(es_userdead) = 0) do
	{
		es_delayed .2 wcs_setfx gravity event_var(userid) = .65 0
		es wcsgroup set evasion event_var(userid) 1
		es wcsgroup set evasion_chance event_var(userid) server_var(wcs_chance)
	    es wcsgroup get evasion_count wcs_tmp1 event_var(userid)
	    es_tell event_var(userid) #multi #green [WCS]: #lightgreen Evade - You evaded server_var(wcs_tmp1) attacks last round
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen Evade - You jump 35% higher and have server_var(wcs_chance)% chance to evade
	}
}

block stomp
{
	es_xset wcs_execute_ultimate 0
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius) "es_xdoblock wcs/tools/manifest/mr_x/stomp_path"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Water Stomp affected server_var(wcs_count_players_actually_affected) enemies
				es wcs_centertell server_var(wcs_userid) Water stomp dealt 20 damage to server_var(wcs_count_players_actually_affected) enemies
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, no nearby enemies!
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
}

block stomp_path 
{
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es_xmath wcs_counter + 1
				if (server_var(wcs_counter) > 0) do
				{
					if (server_var(wcs_counter) <= server_var(wcs_maxtargets)) do
					{
						es_set wcs_initial_players_struck server_var(wcs_counter)
						es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
						if (server_var(wcs_tmp) != 1) do
						{
							es_xset wcs_ultinotexec 0
							es_xset wcs_execute_ultimate 1
							es_xmath wcs_count_players_actually_affected + 1
							es_xdoblock wcs/tools/manifest/mr_x/stomp_final
						}
						if (server_var(wcs_tmp) == 1) do
						{
							es_xmath wcs_count_players_ultimate_immunity + 1
							if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_targetid) #multi #green [WCS]: #lightgreen Ultimate immunity - You blocked an enemy's ultimate
								es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
							}
							if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, all nearby enemy targets were immune!
								es_xset wcs_ultinotexec 1
							}
						}
					}
					else do
					{
						es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, you can't hit that many targets at once!
						es_xset wcs_ultinotexec 1
					}
				}
			}
		}	
	}
}

block stomp_final
{
	es wcs_pushed server_var(wcs_targetid) 0 0 475
	es wcsgroup get is_frozen wcs_tmp4 server_var(wcs_targetid)
	if (server_var(wcs_tmp4) == 1) do
	{
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - A target was already immobilized or immune to immobilizing!
	}
	if (server_var(wcs_tmp4) == 0) do
	{
		es wcsgroup set is_frozen server_var(wcs_targetid) 1
		es_delayed .7 wcsgroup set is_frozen server_var(wcs_targetid) 0
		es_delayed .2 wcs_setfx freeze server_var(wcs_targetid) = 1 .5
	}
	es_tell server_var(wcs_targetid) #multi #green [WCS]: #lightgreen Water Stomp knocked you up and did 20 damage 
	es wcs_centertell server_var(wcs_targetid) You took 20 damage from Water stomp
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid)
	es est_effect 10 #a 0 sprites/crystal_beam1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 100 1000 2 80 10 0 0 50 180 85 5
	es est_effect 10 #a 0 sprites/combineball_trail_blue_1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 21 22 2 15 10 2 0 50 180 150 3
	es_xmath wcs_z1 + 8
	es est_effect 10 #a 0 sprites/crystal_beam1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 40 200 2 20 10 0 0 50 180 85 5
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/crystal_beam1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 40 200 2 20 10 0 0 50 180 85 5	
	es_xmath wcs_z1 + 20
	es_xmath wcs_z2 + 20
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect_06 #a 0 sprites/crystal_beam1.vmt server_var(vector1) server_var(vector2) 5 1 14 14 0 0 0 50 190 180 27
	es est_effect_06 #a 0 sprites/tp_beam001.vmt server_var(vector1) server_var(vector2) 5 1 10 10 0 0 0 60 225 210 5
	es damage server_var(wcs_targetid) 20 1 server_var(wcs_userid)
}