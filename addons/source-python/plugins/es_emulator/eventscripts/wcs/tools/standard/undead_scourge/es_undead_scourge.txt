//////////////////////
//Standard Race Pack//
//Version 1.0.0D   //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Spawn (START)
	}
	if (event_var(userid) > 0) do
	{
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_xmath wcs_z1 + 5
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 10 40 3.6 10 50 2 155 0 0 255 2
		es_xmath wcs_z1 + 10
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 10 50 3.6 10 50 2 155 0 0 255 2
		es_xmath wcs_z1 + 10
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 10 60 3.6 10 50 2 155 0 0 255 2
		es_xmath wcs_z1 + 10
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 10 70 3.6 10 50 2 155 0 0 255 2
		es_xmath wcs_z1 + 10
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 10 80 3.6 10 50 2 155 0 0 255 2
		es_xmath wcs_z1 + 10
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 10 90 3.6 10 50 2 155 0 0 255 2
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Spawn (END)
	}
}

block vampire
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do 
			{
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z1 + 20
				es_xmath wcs_z2 + 20
				es est_effect 3 #a 0 sprites/shellchrome.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 10 10 255 0 0 255
				es est_effect 3 #a 0 sprites/tp_beam001.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 10 10 255 0 0 255
				es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 2 2 255 255 255 255
				es_xmath wcs_z2 + 8
				es est_effect 10 #a 0 sprites/shellchrome.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 10 0.5 40 10 255 255 0 0 100 255
				es playerget health wcs_tmp2 event_var(attacker)
				if (server_var(wcs_tmp2) < 260) do
				{
					es_xrand wcs_addhealth 12 20
					es wcs_setfx health event_var(attacker) + server_var(wcs_addhealth) 0
				    es wcs_getlanguage wcs_lng "player_skill1_leech" 0 "undead_scourge"
				    es wcs_xcentertell event_var(attacker) server_var(wcs_lng) value1 server_var(wcs_addhealth)
					es playerget health wcs_tmp2 event_var(attacker)
					if (server_var(wcs_tmp2) > 260) do
					{
						es wcs_setfx health event_var(attacker) = 260 0
					}
				}
				if (server_var(wcs_tmp2) => 260) do
				{
					es wcs_setfx health event_var(attacker) = 260 0
				    es wcs_getlanguage wcs_lng "player_skill1_cap" 0 "undead_scourge"
				    es wcs_xcentertell event_var(attacker) server_var(wcs_lng)
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 1 (END)
	}
}

block unholy
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 2 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_xmath wcs_speed * 100
	es_xmath wcs_speed - 100
	es wcs_decimal wcs_speed server_var(wcs_speed)
    es wcs_getlanguage wcs_lng "player_skill2" 0 "undead_scourge"
    es wcs_xtell event_var(userid) server_var(wcs_lng) value1 server_var(wcs_speed)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 2 (END)
	}
}

block levitation
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 3 (START)
	}
	es_delayed .2 wcs_setfx gravity event_var(userid) = server_var(wcs_gravity) 0
	es_xmath wcs_gravity * 100
	es wcs_decimal wcs_gravity server_var(wcs_gravity)
    es wcs_getlanguage wcs_lng "player_skill3" 0 "undead_scourge"
    es wcs_xtell event_var(userid) server_var(wcs_lng) value1 server_var(wcs_gravity)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 3 (END)
	}
}

block bomb
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice4) <= server_var(wcs_chance)) do
	{
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_xmath wcs_z1 + 35
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector1) 1 10 5 200 10 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) metal
		ifx true(wcs_ffa_enabled) do
		{
			es_xset wcs_targetteam "#alive"
			es_getlivingplayercount wcs_pcount
		}
		ifx false(wcs_ffa_enabled) do
		{
			es_getplayerteam wcs_team event_var(userid)
			if (server_var(wcs_team) = 2) do
			{
				es_xset wcs_targetteam "#ct,#alive"
				es_getlivingplayercount wcs_pcount 3
			}	
			if (server_var(wcs_team) = 3) do
			{
				es_xset wcs_targetteam "#t,#alive"
				es_getlivingplayercount wcs_pcount 2
			}
		}
		if (server_var(wcs_targetteam) != 0) do
		{	
			if (server_var(wcs_pcount) > 0) do
			{
				es_xset wcs_counter 0
				es_xset wcs_initial_players_struck 0
				es_xset wcs_count_players_ultimate_immunity 0
				es_xset wcs_count_players_actually_affected 0
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius) "es_xdoblock wcs/tools/standard/undead_scourge/wcs_ulti_suicide_checks"
				if (server_var(wcs_execute_ultimate) == 1) do
				{
					//No matter how many targets are affected, this section will only be executed once.
					//If you have a global counter you wish to retrieve then use this section, also good for general mesasges or commands you only want to happen on the wcs_userid / event_var(userid)
				    es wcs_getlanguage wcs_lng "player_skill4_centertell" 0 "undead_scourge"
				    es wcs_xcentertell event_var(userid) server_var(wcs_lng) value1 server_var(wcs_count_players_actually_affected) value2 server_var(wcs_magnitude)
				    es wcs_getlanguage wcs_lng "player_skill4_tell" 0 "undead_scourge"
				    es wcs_xtell event_var(userid) server_var(wcs_lng) value1 server_var(wcs_count_players_actually_affected)
					es_xrand wcs_rand 1 3
					if (server_var(wcs_rand) == 1) do
					{
						es_playsound event_var(userid) weapons\hegrenade\explode5.wav .7
					}
					if (server_var(wcs_rand) == 2) do
					{
						es_playsound event_var(userid) weapons\hegrenade\explode4.wav .8
					}
					if (server_var(wcs_rand) == 3) do
					{
						es_playsound event_var(userid) weapons\hegrenade\explode3.wav .7
					}
				}
			}
			else do
			{
			    es wcs_getlanguage wcs_lng "check_skill4_failed_living" 0 "undead_scourge"
			    es wcs_xtell event_var(userid) server_var(wcs_lng)
			}
			if (server_var(wcs_counter) = 0) do
			{
			    es wcs_getlanguage wcs_lng "check_skill4_failed_nearby" 0 "undead_scourge"
			    es wcs_xtell event_var(userid) server_var(wcs_lng)
			}
		}
		else do
		{
		    es wcs_getlanguage wcs_lng "check_skill4_failed_targeting" 0 "undead_scourge"
		    es wcs_xtell event_var(userid) server_var(wcs_lng)
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 4 (END)
	}
}

block wcs_ulti_suicide_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 4 (CHECKS START)
	}
	es_exists ex userid server_var(wcs_targetid)
	if (server_var(ex) = 1) do
	{
		es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
		if (server_var(wcs_alive) = 0) do
		{
			es_xmath wcs_counter + 1
			if (server_var(wcs_counter) > 0) do
			{
				if (server_var(wcs_counter) <= 20) do
				{
					es_set wcs_initial_players_struck server_var(wcs_counter)
					es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
					if (server_var(wcs_tmp) != 1) do
					{
						es_xset wcs_execute_ultimate 1
						es_xmath wcs_count_players_actually_affected + 1
						es_xdoblock wcs/tools/standard/undead_scourge/wcs_ulti_suicide_final
					}
					if (server_var(wcs_tmp) == 1) do
					{
						es_xmath wcs_count_players_ultimate_immunity + 1
						if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
						{
							es wcs_getlanguage wcs_lng "check_skill4_failed_target_is_immune" 0 "undead_scourge"
				    		es wcs_xtell server_var(wcs_targetid) server_var(wcs_lng)
							es wcs_getlanguage wcs_lng "check_skill4_failed_some_are_immune" 0 "undead_scourge"
				    		es wcs_xtell event_var(userid) server_var(wcs_lng) value1 server_var(wcs_count_players_ultimate_immunity)
						}
						if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
						{
							es wcs_getlanguage wcs_lng "check_skill4_failed_all_are_immune" 0 "undead_scourge"
				    		es wcs_xtell event_var(userid) server_var(wcs_lng)
						}
					}
				}
				else do
				{
					es wcs_getlanguage wcs_lng "check_skill4_failed_too_many_targets" 0 "undead_scourge"
		    		es wcs_xtell event_var(userid) server_var(wcs_lng)
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 4 (CHECKS END)
	}
}

block wcs_ulti_suicide_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 4 (EXECUTED START)
	}
    es wcs_getlanguage wcs_lng "target_skill4_centertell" 0 "undead_scourge"
    es wcs_xcentertell event_var(userid) server_var(wcs_lng) value1 server_var(wcs_magnitude)
	es_xrand wcs_rand 1 3
	if (server_var(wcs_rand) == 1) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode5.wav .7
	}
	if (server_var(wcs_rand) == 2) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode4.wav .8
	}
	if (server_var(wcs_rand) == 3) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode3.wav .7
	}
	es damage server_var(wcs_targetid) server_var(wcs_magnitude) 1 event_var(userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Undead Scourge - [Block] Skill: 4 (EXECUTED END)
	}
}