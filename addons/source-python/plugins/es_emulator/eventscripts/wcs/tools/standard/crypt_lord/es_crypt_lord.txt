//////////////////////
//Standard Race Pack//
//Version 1.0.1D    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Spawn (START)
	}
	es wcsgroup set spiked_carapace event_var(userid) 0
	es wcsgroup set absorb event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Death (START)
	}
	es wcsgroup set spiked_carapace event_var(userid) 0
	es wcsgroup set absorb event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] OnChange (START)
	}
	es wcsgroup set spiked_carapace server_var(wcs_userid) 0
	es wcsgroup set absorb server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] OnChange (END)
	}
}

block shake
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcs_shake event_var(userid) 22 15 3
				es wcs_pushed event_var(userid) 0 0 520
				es wcs_getlanguage wcs_lng "player_skill1" 0 "crypt_lord"
				es wcs_xtell event_var(attacker) server_var(wcs_lng)
				es wcs_getlanguage wcs_lng "target_skill1" 0 "crypt_lord"
				es wcs_xtell event_var(userid) server_var(wcs_lng)
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es est_effect 10 #a 0 sprites/plasmaember.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 80 0.5 80 80 0 255 255 0 100 10
				es_xmath wcs_z2 + 325
				es_xmath wcs_x2 + 70
				es est_effect 3 #a 0 effects/bluespark.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 20 11 255 255 255 255
				es_xmath wcs_y2 + 90
				es_xmath wcs_x2 - 20
				es est_effect 3 #a 0 effects/energysplash.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 20 11 255 255 255 255
				es est_effect 3 #a 0 effects/gunshiptracer.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 20 11 255 255 255 255
				es_xmath wcs_y2 - 140
				es_xmath wcs_x2 - 60
				es est_effect 3 #a 0 effects/gunshiptracer.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 20 11 255 255 255 255
				es est_effect 3 #a 0 effects/bluespark.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 20 11 255 255 255 255
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 1 (END)
	}
}

block armor
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 2 (START)
	}
	es wcsgroup set spiked_carapace event_var(userid) 1
	es wcsgroup set absorb event_var(userid) server_var(wcs_gravity)
	es wcs_getlanguage wcs_lng "player_skill2_tell" 0 "crypt_lord"
	es wcs_xtell event_var(userid) server_var(wcs_lng)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 2 (END)
	}
}

event player_attacker
{
	es wcsgroup get spiked_carapace wcs_tmp4 event_var(userid)
	if (server_var(wcs_tmp4) == 0) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Crypt Lord - [Block] Event Player_Attacker (START)
		}
		es_xrand wcs_rand 3 7
		es damage event_var(attacker) server_var(wcs_rand) 1 event_var(userid)
		es wcs_getlanguage wcs_lng "player_skill2_centertell" 0 "crypt_lord"
		es wcs_xcentertell event_var(attacker) server_var(wcs_lng) value1 server_var(wcs_rand)
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Crypt Lord - [Block] Event Player_Attacker (END)
		}
	}
}

block damage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_xrand wcs_dmg 12 17
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_getlanguage wcs_lng "player_skill3" 0 "crypt_lord"
				es wcs_xcentertell event_var(userid) server_var(wcs_lng) value1 server_var(wcs_dmg)
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
				es est_effect_14 #a 0 effects/blueflare1.vmt server_var(vector2) server_var(vector1) 225 24 130
				es est_effect_14 #a 0 effects/yellowflare_noz.vmt server_var(vector2) server_var(vector1) 225 16 120
				es est_effect_14 #a 0 effects/blueblackflash_v2.vmt server_var(vector2) server_var(vector1) 225 14 100
				es est_effect_14 #a 0 effects/redflare_v2.vmt server_var(vector2) server_var(vector1) 225 22 125
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 3 (END)
	}
}

block swarm
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 4 (START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es wcs_randplayer wcs_targetid server_var(wcs_targetteam)
			if (server_var(wcs_targetid) != 0) do
			{
				es_xdoblock wcs/tools/standard/crypt_lord/ultimate_checks
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es wcs_getlanguage wcs_lng "check_skill4_failed_living" 0 "crypt_lord"
			es wcs_xtell server_var(wcs_userid) server_var(wcs_lng)
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es wcs_getlanguage wcs_lng "check_skill4_failed_targeting" 0 "crypt_lord"
		es wcs_xtell server_var(wcs_userid) server_var(wcs_lng)
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 4 (END)
	}
}

block ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 4 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
				if (server_var(wcs_tmp) != 1) do
				{
					es_xset wcs_ultinotexec 0
					es_xdoblock wcs/tools/standard/crypt_lord/ultimate_final
				}
				if (server_var(wcs_tmp) == 1) do
				{
					es wcs_getlanguage wcs_lng "check_skill4_failed_target_is_immune" 0 "crypt_lord"
					es wcs_xtell server_var(wcs_targetid) server_var(wcs_lng)
					es wcs_getlanguage wcs_lng "check_skill4_failed_player_is_immune" 0 "crypt_lord"
					es wcs_xtell server_var(wcs_userid) server_var(wcs_lng)
					es_xset wcs_ultinotexec 1
				}
			}
		}	
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 4 (CHECKS END)
	}
}

block ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 4 (EXECUTED START)
	}
	es playerget health wcs_tmp1 server_var(wcs_userid)
	es_xmath wcs_tmp1 + 35
	if (server_var(wcs_tmp1) >= 225) do
	{
		es wcs_setfx health server_var(wcs_userid) = 225 0
		es wcs_getlanguage wcs_lng "player_skill4_health_cap" 0 "crypt_lord"
		es wcs_xtell server_var(wcs_userid) server_var(wcs_lng)
	}
	if (server_var(wcs_tmp1) < 225) do
	{
		es wcs_getlanguage wcs_lng "player_skill4_health" 0 "crypt_lord"
		es wcs_xtell server_var(wcs_userid) server_var(wcs_lng)
		es wcs_setfx health server_var(wcs_userid) + 35 0
	}
	es playerget armor wcs_tmp2 server_var(wcs_userid)
	es_math wcs_tmp2 + server_var(wcs_armor)
	if (server_var(wcs_tmp2) >= 250) do
	{
		es wcs_setfx armor server_var(wcs_userid) = 250 0
		es wcs_getlanguage wcs_lng "player_skill4_armor_cap" 0 "crypt_lord"
		es wcs_xtell server_var(wcs_userid) server_var(wcs_lng)
	}
	if (server_var(wcs_tmp2) < 250) do
	{
		es wcs_setfx armor server_var(wcs_userid) + server_var(wcs_armor) 0
		es wcs_getlanguage wcs_lng "player_skill4_armor" 0 "crypt_lord"
		es wcs_xtell server_var(wcs_userid) server_var(wcs_lng) value1 server_var(wcs_armor)
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 70 1 12 10 .8 0 50 200 200 1
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 70 1 12 10 .8 0 50 200 200 1
	es damage server_var(wcs_targetid) 35 1 server_var(wcs_userid)
	es wcs_getlanguage wcs_lng "player_skill4_centertell" 0 "crypt_lord"
	es wcs_xcentertell server_var(wcs_userid) server_var(wcs_lng)
	es wcs_getlanguage wcs_lng "target_skill4_centertell" 0 "crypt_lord"
	es wcs_xcentertell server_var(wcs_targetid) server_var(wcs_lng)
	es_xmath wcs_smokestack_counter + 1
	es_give server_var(wcs_targetid) env_smokestack
	es_format ss_name "my_ss_%1%2" server_var(wcs_targetid) server_var(wcs_smokestack_counter)
	es_entitysetvalue server_var(eventscripts_lastgive) "targetname" server_var(ss_name)
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "basespread 55"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "spreadspeed 60"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "speed 30"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "rate 60"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "startsize 1"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "endsize 2"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "twist 150"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "jetlength 50"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "angles 90 90 90"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "rendercolor 255 255 255"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "SmokeMaterial sprites/scanner.vmt"
	es_fire server_var(wcs_targetid) server_var(ss_name) turnon
	es_delayed 1 es_fire server_var(wcs_targetid) server_var(ss_name) kill
	es_playsound server_var(wcs_targetid) ambient\creatures\flies4.wav 1
	es_delayed .2 es_playsound server_var(wcs_targetid) ambient\creatures\flies3.wav 1
	es_delayed .5 es_playsound server_var(wcs_targetid) ambient\creatures\flies2.wav 1
	es_playsound server_var(wcs_userid) ambient\creatures\flies4.wav .7
	es_delayed .2 es_playsound server_var(wcs_userid) ambient\creatures\flies3.wav .8
	es_delayed .5 es_playsound server_var(wcs_userid) ambient\creatures\flies2.wav .7
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Crypt Lord - [Block] Skill: 4 (EXECUTED END)
	}
}
