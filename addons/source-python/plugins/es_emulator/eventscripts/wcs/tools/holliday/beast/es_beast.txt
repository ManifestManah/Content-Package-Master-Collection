//////////////////////
//Holliday Race Pack//
//Version 1.0.0D    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Spawn (START)
	}
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_playsound event_var(userid) npc/ichthyosaur/attack_growl1.wav 1
		es_delayed 1.5 es_playsound event_var(userid) npc/ichthyosaur/attack_growl3.wav 1
	}
	es_delayed .5 wcs_color event_var(userid) 255 55 0 155 1
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 500 3 100 100 0 255 55 0 255 10
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Spawn (END)
	}
}

block rage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			es_getgame wcs_gamecheck
			if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
			{
				es_playsound event_var(userid) ambient/levels/prison/inside_battle_antlion3.wav 1
			}
			es_xrand wcs_dmg 4 11
			es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
			es wcs_centertell event_var(attacker) Rage of the beast did + server_var(wcs_dmg) damage
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
			es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
			es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 500 3 100 100 0 255 55 0 255 10
			es_xmath wcs_z1 + 20
			es_xmath wcs_z2 + 20
			es est_effect 3 #a 0 sprites/glow.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 20 60 255 50 0 255
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 1 (END)
	}
}

block resurrection
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		es_delayed 1.5 wcs_spawn event_var(userid) 1
		es_getgame wcs_gamecheck
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_playsound event_var(userid) npc/antlion/attack_single4.wav 1
		}
		es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
		es est_effect 11 #a 0 sprites/fire2.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 3 1 255
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 20 500 3 100 100 0 255 55 0 255 10
		es_delayed 1.5 wcs_teleport event_var(userid) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen Resurrection of the beast - Resurrecting you where you died in 1.5 second
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 2 (END)
	}
}

block burning_blind
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			es wcsgroup get is_burning wcs_tmp3 event_var(userid)
			ifx false(wcs_tmp3) do
			{
				es wcsgroup set is_burning event_var(userid) 1
				es_delayed 3 wcsgroup set is_burning event_var(userid) 0
				es_cexec event_var(userid) r_screenoverlay cs_italy/black.vmt
				es_delayed .75 es_cexec event_var(userid) r_screenoverlay 0
				es wcsgroup set fire_owner event_var(userid) event_var(attacker)
				es wcs_setfx burn event_var(userid) = 1 3
				es_tell event_var(userid) #multi #green [WCS]: #lightgreen Burning blind - You were blinded and set ablaze
				es_tell event_var(attacker) #multi #green [WCS]: #lightgreen Burning blind - Blinded the enemy and set them ablaze
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(attacker) npc/ichthyosaur/attack_growl3.wav .6
					es_playsound event_var(userid) npc/ichthyosaur/attack_growl3.wav 1
				}
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es est_effect 11 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 3 3 255
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 20 500 3 100 100 0 255 55 0 255 10
				es_xmath wcs_z2 + 10
				es est_effect 10 #a 0 sprites/fire1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 50 200 1 40 10 0 255 255 255 255 8
				es_xmath wcs_z2 + 30
				es est_effect 10 #a 0 sprites/fire1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 50 200 1 20 10 0 255 255 255 255 8
				es_xmath wcs_z2 + 40
				es est_effect 11 #a 0 sprites/fire1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 1 255
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 3 (END)
	}
}

block teleport_push
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 4 (START)
	}
    es_xset wcs_ok 1

    es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
    es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 20 500 3 100 100 0 255 55 0 255 10

    es_xcopy wcs_x wcs_x1
    es_xcopy wcs_y wcs_y1
    es_xcopy wcs_z wcs_z1

    es_math wcs_x - server_var(wcs_x2)
    es_math wcs_y - server_var(wcs_y2)
    es_math wcs_z - server_var(wcs_z2)

    if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
    if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
    if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1

    if (server_var(wcs_range) < server_var(wcs_x)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_y)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_z)) do
    {
		es_xset wcs_ok 0
    }
    
    ifx true(wcs_ok) do
    {
 	    ifx true(wcs_legacy_teleport_type) do
	    {
	    	//To Do: Improve this by finding an alternative teleportation method that prevent players from getting stuck in displacemnts and models
	    	es playerset location server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    	es_delayed .01 wcs_teleport server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    }
	    ifx false(wcs_legacy_teleport_type) do
	    {
	    	es_setplayerprop server_var(wcs_userid) CBasePlayer.localdata.m_vecBaseVelocity 0,0,250
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) 2
	    }
	    es wcs_fade server_var(wcs_userid) 30 0 0 175 1 4
        es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreenTeleport - You Teleported!
		es_getgame wcs_gamecheck
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_userid) npc/ichthyosaur/attack_growl3.wav .8
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
		}
    }
    
    else do
    {
        if (server_var(wcs_range) < server_var(wcs_x)) do
        {
            if (server_var(wcs_x2) >= 0) do
            {
                if (server_var(wcs_x1) >= 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_x1) < 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_x1 wcs_x2
        }
        
        if (server_var(wcs_range) < server_var(wcs_y)) do
        {
            if (server_var(wcs_y2) >= 0) do
            {
                if (server_var(wcs_y1) >= 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_y1) < 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_y1 wcs_y2
        }
        
        if (server_var(wcs_range) < server_var(wcs_z)) do
        {
            if (server_var(wcs_z2) >= 0) do
            {
                if (server_var(wcs_z1) >= 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_z1) < 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_z1 wcs_z2
        }

	    ifx false(wcs_legacy_teleport_type) do
	    {
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) .1
	    }
	    es wcs_fade server_var(wcs_userid) 30 0 0 175 1 4
        es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreenTeleport - You cannot teleport that far!
	    es wcs_setcooldown server_var(wcs_userid) 0
    }
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Beast - [Block] Skill: 4 (END)
	}
}
