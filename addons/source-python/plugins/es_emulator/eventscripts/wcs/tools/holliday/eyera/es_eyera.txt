//////////////////////
//Holliday Race Pack//
//Version 1.1.0A    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Spawn (START)
	}
	es wcsgroup set evasion event_var(userid) 0
	es wcsgroup set evasion_chance event_var(userid) 0
	es_delayed .1 wcs_setcooldown event_var(userid) 25
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Spawn (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] OnChange (START)
	}
	es wcsgroup set evasion server_var(wcs_userid) 0
	es wcsgroup set evasion_chance server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] OnChange (END)
	}
}

block respawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		es_getplayerlocation wcs_x3 wcs_y3 wcs_z3 event_var(userid)
		es_xmath wcs_z3 + 15
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x3) server_var(wcs_y3) server_var(wcs_z3) 750 100 3 75 10 0 55 155 255 150 1
		es_xmath wcs_z3 - 15
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Reincarnation - Respawning where you died in 3 seconds!
		es_delayed 2.5 es_playsound event_var(userid) wcs/resurrect.mp3 .8
		es_delayed 2.5 wcs_spawn event_var(userid) 1
		es_delayed 2.6 wcs_teleport event_var(userid) server_var(wcs_x3) server_var(wcs_y3) server_var(wcs_z3)
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 1 (END)
	}
}

block blink
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 2 (START)
	}
	es wcsgroup set evasion event_var(userid) 1
	es wcsgroup set evasion_chance event_var(userid) server_var(wcs_chance)
    es wcsgroup get evasion_count wcs_tmp1 event_var(userid)
    es_tell event_var(userid) #multi #green [WCS]#lightgreen Blink of an eye - You evaded server_var(wcs_tmp1) attacks last round
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Blink of an eye - Gave you server_var(wcs_chance)% to evade
	es_playsound event_var(userid) weapons\fx\nearmiss\bulletltor13.wav 1
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
	es_xmath wcs_z1 + 50
	es_xmath wcs_y1 + 100
	es_xmath wcs_x1 + 100
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 2 20 10 55 155 255 150
	es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector2) server_var(vector1) 10 2 20 10 10 0 55 155 255 255 9
	es_xmath wcs_y1 - 200
	es_xmath wcs_x1 - 200
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 2 20 10 255 255 0 150
	es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector2) server_var(vector1) 10 2 20 10 10 0 255 255 0 255 9
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 2 (END)
	}
}

block truth
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			es wcsgroup get is_already_colored wcs_tmp6 event_var(userid)
			if (server_var(wcs_tmp6) == 0) do
			{
				es wcsgroup set is_already_colored event_var(userid) 1
				es_delayed 2 es wcsgroup set is_already_colored event_var(userid) 0
				es wcs_getcolors event_var(userid) wcs_red wcs_green wcs_blue wcs_alpha
				ifx true(wcs_ffa_enabled) do
				{
					es_delayed .1 wcs_color event_var(userid) 255 0 0 255 1
				}
				ifx false(wcs_ffa_enabled) do
				{
					if (event_var(es_userteam) = 2) then es_delayed .1 wcs_color event_var(userid) 255 0 0 255 1
					if (event_var(es_userteam) = 3) then es_delayed .1 wcs_color event_var(userid) 0 255 0 255 1
				}
				es_delayed 2 wcs_color event_var(userid) server_var(wcs_red) server_var(wcs_green) server_var(wcs_blue) server_var(wcs_alpha) 1
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z1 + 25
				es_xmath wcs_z2 + 25
				es est_effect 3 #a 0 sprites/orangelight1_v2.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 3 6 12 155 155 155 255
				es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 2 20 10 55 155 255 150
				es est_effect 11 #a 0 sprites/halo.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 3 2 155
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 3 (END)
	}
}

block summon
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 4 (START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es wcs_randplayer wcs_targetid server_var(wcs_targetteam)
			if (server_var(wcs_targetid) != 0) do
			{
				es_xdoblock wcs/tools/holliday/eyera/ultimate_checks
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, there are no living enemies!
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 4 (END)
	}
}

block ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 4 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
				if (server_var(wcs_tmp) != 1) do
				{
					es_xset wcs_ultinotexec 0
					es_xdoblock wcs/tools/holliday/eyera/ultimate_final
				}
				if (server_var(wcs_tmp) == 1) do
				{
					es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, the targeted enemy was immune!
					es_xset wcs_ultinotexec 1
				}
			}
		}	
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 4 (CHECKS END)
	}
}

block ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 4 (EXECUTED START)
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid)
	es_delayed 3 wcs_teleport server_var(wcs_targetid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	
	es_xmath wcs_z1 + 35
	es_xmath wcs_z2 + 35
	es_delayed 3 playerset viewcoord server_var(wcs_userid) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es_delayed 3 playerset viewcoord server_var(wcs_targetid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es wcs_centertell server_var(wcs_targetid) Eye Ra will summon you in 3 seconds!
	es_delayed 1 wcs_centertell server_var(wcs_targetid) Eye Ra will summon you in 2 seconds!
	es_delayed 2 wcs_centertell server_var(wcs_targetid) Eye Ra will summon you in 1 seconds!
	es_delayed 3 wcs_centertell server_var(wcs_targetid) Eye Ra has summoned you to him!
	es wcs_centertell server_var(wcs_userid) Eye Ra will summon you in 3 seconds!
	es_delayed 1 wcs_centertell server_var(wcs_userid) Eye Ra will summon you in 2 seconds!
	es_delayed 2 wcs_centertell server_var(wcs_userid) Eye Ra will summon you in 1 seconds!
	es_delayed 3 wcs_centertell server_var(wcs_userid) Eye Ra has summoned you to him!
	es_playsound server_var(wcs_userid) ambient\machines\wall_move5.wav 1
	es_playsound server_var(wcs_targetid) ambient\machines\wall_move5.wav 1
	es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Summon - You have summoned an enemy to fight you
	es_delayed 3 es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Summon - Your enemy has arrived for battle!
	es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Summon - You have been summoned to fight Eye Ra
	es_delayed 3 es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Summon - You've arrived, prepare for battle!
	es_xmath wcs_z1 + 5
	es_xmath wcs_z2 + 5
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 950 190 3 128 100 0 155 115 100 200 1
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 950 190 3 128 100 0 155 115 100 200 1
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eye Ra - [Block] Skill: 4 (EXECUTED END)
	}
}