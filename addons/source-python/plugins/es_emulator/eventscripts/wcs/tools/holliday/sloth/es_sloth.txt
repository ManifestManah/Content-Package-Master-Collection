//////////////////////
//Holliday Race Pack//
//Version 1.0.0D    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;deagle;elite;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;g3sg1;m249"
		es_delayed .1 es_give event_var(userid) weapon_sg552
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;hkp2000;elite;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;ssg08;m249;negev"
		es_delayed .5 es_give event_var(userid) weapon_sg556
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_delayed .5 es_give event_var(userid) weapon_fiveseven
	es wcs_setfx speed event_var(userid) = .95 0
	es wcsgroup set hell_sloth event_var(userid) 1
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Death (START)
	}
	es wcsgroup set hell_sloth event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] OnChange (START)
	}
	es wcs_unrestrict server_var(wcs_userid) all
	es wcsgroup set hell_sloth server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] OnChange (END)
	}
}

block paralyze
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		es wcsgroup get is_paralyzed wcs_tmp3 event_var(userid)
		if (server_var(wcs_tmp3) == 0) do
		{
			es wcsgroup set is_paralyzed event_var(userid) 1
			es_delayed .5 wcsgroup set is_paralyzed event_var(userid) 0
			es_setplayerprop event_var(userid) CBasePlayer.m_fFlags 64
			es_delayed .5 es_setplayerprop event_var(userid) CBasePlayer.m_fFlags 131329
			es_tell event_var(userid) #multi #green [WCS]: #lightgreen Paralyze - You are paralyzed for a half second
			es_tell event_var(attacker) #multi #green [WCS]: #lightgreen Paralyze - You paralyzed the enemy for a half second
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
			es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
			es_xmath wcs_z1 + 500
			es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
			es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
			es est_effect_06 #a 0 sprites/hydraspinalcord.vmt server_var(vector1) server_var(vector2) 1 3 15 1 10 10 228 228 228 255 10
			es est_effect_14 #a 0 models/airboat/airboat_blur02.vmt server_var(vector2) server_var(vector2) 200 309 90
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 1 (END)
	}
}

block struck
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		es_xrand wcs_dmg 3 9
		es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
		es wcs_centertell event_var(attacker) Hell stuck dealt + server_var(wcs_dmg) damage
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
		es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
		es_xmath wcs_z1 + 20
		es_xmath wcs_z2 + 20
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		es est_effect_06 #a 0 sprites/hydraspinalcord.vmt server_var(vector1) server_var(vector2) 1 1 15 1 10 10 228 228 228 255 10
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 2 (END)
	}
}

block hell
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do 
	{
		es_xrand wcs_dmg 20 42
		es damage event_var(attacker) server_var(wcs_dmg) 1 event_var(userid)
		es wcs_centertell event_var(attacker) From hell dealt server_var(wcs_dmg) damage to you
		es wcs_centertell event_var(userid) From hell reflected server_var(wcs_dmg) damage to the attacker
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
		es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
		es_xmath wcs_z1 + 700
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		es est_effect_06 #a 0 sprites/hydraspinalcord.vmt server_var(vector1) server_var(vector2) 100 .3 15 1 10 10 228 228 228 255 100
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 3 (END)
	}
}

block sloth
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 4 (START)
	}
	es wcsgroup get hell_sloth wcs_tmp3 server_var(wcs_userid)
	if (server_var(wcs_tmp3) == 1) do
	{
		es wcs_cancelulti server_var(wcs_userid)

		es wcsgroup set hell_sloth server_var(wcs_userid) 2
	    es wcs_removeweapon server_var(wcs_userid) 1
		es wcs_removeweapon server_var(wcs_userid) 2
		es wcs_removeweapon server_var(wcs_userid) 3
		es wcs_color server_var(wcs_userid) 255 255 255 26 1
		es wcs_setfx speed server_var(wcs_userid) + server_var(wcs_speed) 0
		es_getgame wcs_gamecheck
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_userid) npc/strider/strider_legstretch1.wav 1
			es wcs_restrict server_var(wcs_userid) "knife;glock;usp;p228;deagle;elite;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;sg552;g3sg1;m249"
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es wcs_restrict server_var(wcs_userid) "knife;glock;usp_silencer;p250;hkp2000;elite;fiveseven;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;ssg08;m249;negev"
			es_playsound server_var(wcs_userid) wcs/shadow.mp3 .8
		}
	    es_xset wcs_ok 1

	    es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
	    es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
		es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		es est_effect_18 #a 0 server_var(vector2) 255 155 75 50 150 30 75

	    es_xcopy wcs_x wcs_x1
	    es_xcopy wcs_y wcs_y1
	    es_xcopy wcs_z wcs_z1

	    es_math wcs_x - server_var(wcs_x2)
	    es_math wcs_y - server_var(wcs_y2)
	    es_math wcs_z - server_var(wcs_z2)

	    if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
	    if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
	    if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1

	    if (server_var(wcs_range) < server_var(wcs_x)) do
	    {
			es_xset wcs_ok 0
	    }
	    if (server_var(wcs_range) < server_var(wcs_y)) do
	    {
			es_xset wcs_ok 0
	    }
	    if (server_var(wcs_range) < server_var(wcs_z)) do
	    {
			es_xset wcs_ok 0
	    }
	    
	    ifx true(wcs_ok) do
	    {
	    	es_setplayerprop server_var(wcs_userid) CBasePlayer.localdata.m_vecBaseVelocity 0,0,250
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) 2
	        es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreenTeleporter - You turned invisible and teleported!
	        es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
	    }
	    
	    else do
	    {
	        if (server_var(wcs_range) < server_var(wcs_x)) do
	        {
	            if (server_var(wcs_x2) >= 0) do
	            {
	                if (server_var(wcs_x1) >= 0) do
	                {
	                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
	                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
	                }
	                else do
	                {
	                    es_math wcs_x2 - server_var(wcs_range)
	                }
	            }
	            else do
	            {
	                if (server_var(wcs_x1) < 0) do
	                {
	                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
	                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
	                }
	                else do
	                {
	                    es_math wcs_x2 + server_var(wcs_range)
	                }
	            }
	            es_xcopy wcs_x1 wcs_x2
	        }
	        
	        if (server_var(wcs_range) < server_var(wcs_y)) do
	        {
	            if (server_var(wcs_y2) >= 0) do
	            {
	                if (server_var(wcs_y1) >= 0) do
	                {
	                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
	                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
	                }
	                else do
	                {
	                    es_math wcs_y2 - server_var(wcs_range)
	                }
	            }
	            else do
	            {
	                if (server_var(wcs_y1) < 0) do
	                {
	                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
	                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
	                }
	                else do
	                {
	                    es_math wcs_y2 + server_var(wcs_range)
	                }
	            }
	            es_xcopy wcs_y1 wcs_y2
	        }
	        
	        if (server_var(wcs_range) < server_var(wcs_z)) do
	        {
	            if (server_var(wcs_z2) >= 0) do
	            {
	                if (server_var(wcs_z1) >= 0) do
	                {
	                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
	                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
	                }
	                else do
	                {
	                    es_math wcs_z2 - server_var(wcs_range)
	                }
	            }
	            else do
	            {
	                if (server_var(wcs_z1) < 0) do
	                {
	                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
	                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
	                }
	                else do
	                {
	                    es_math wcs_z2 + server_var(wcs_range)
	                }
	            }
	            es_xcopy wcs_z1 wcs_z2
	        }
	        es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreenTeleporter - You turned invisible but cannot teleport that far!
	    }
	}
	if (server_var(wcs_tmp3) == 2) do
	{
		es_getgame wcs_gamecheck
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es wcs_restrict server_var(wcs_userid) "glock;usp;p228;deagle;elite;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;g3sg1;m249"
			es_give server_var(wcs_userid) weapon_sg552
			es_playsound server_var(wcs_userid) npc/strider/strider_legstretch1.wav 1
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es wcs_unrestrict server_var(wcs_userid) all
			es wcs_restrict server_var(wcs_userid) "glock;usp_silencer;p250;hkp2000;elite;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;ssg08;m249;negev"
			es_give server_var(wcs_userid) weapon_sg556
			es_playsound server_var(wcs_userid) wcs/shadow.mp3 .8
		}
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreenTeleporter - You've turned visible again!
		es_give server_var(wcs_userid) weapon_knife
		es wcs_setfx speed server_var(wcs_userid) - server_var(wcs_speed) 0
		es wcsgroup set hell_sloth server_var(wcs_userid) 1
		es wcs_color server_var(wcs_userid) 255 255 255 255 1
		es_give server_var(wcs_userid) weapon_fiveseven
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Hell Sloth - [Block] Skill: 4 (END)
	}
}