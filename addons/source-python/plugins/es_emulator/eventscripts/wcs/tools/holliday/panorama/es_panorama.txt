//////////////////////
//Holliday Race Pack//
//Version 1.1.0A    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Spawn (START)
	}
	es wcsgroup set panorama_zoom event_var(userid) 1
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_playsound event_var(userid) ambient/atmosphere/cave_hit1.wav 1
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es_playsound event_var(userid) ambient\atmosphere\cs_cable_rattle02.wav 1
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Death (START)
	}
	es wcsgroup set panorama_zoom event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] OnChange (START)
	}
	es wcsgroup set panorama_zoom server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] OnChange (END)
	}
}

block wallclimb
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 1 (START)
	}
	es_setplayerprop event_var(userid) CBasePlayer.localdata.m_Local.m_flStepSize server_var(wcs_range)
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Wall climb - You can now climb over tall objects
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 1 (END)
	}
}

block flipview
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_setplayerprop event_var(userid) cbaseplayer.m_ifov 500
				es_delayed .4 es_setplayerprop event_var(userid) cbaseplayer.m_ifov 0
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(userid) ambient/wind/wind_snippet2.wav 1
					es_playsound event_var(attacker) ambient/wind/wind_snippet2.wav 1
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(userid) ambient\wind\css15_wind_01.wav 1
					es_playsound event_var(attacker) ambient\wind\css15_wind_01.wav 1
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 2 (END)
	}
}

block rematch
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_attackerdead) = 0) do
		{
			es_getgame wcs_gamecheck
			if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
			{
				es_delayed 1.5 es_playsound event_var(userid) weapons/physcannon/superphys_launch2.wav 1
				es_delayed 1.5 es_playsound event_var(attacker) weapons/physcannon/superphys_launch2.wav 1
			}
			if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
			{
				es_delayed 1.5 es_playsound event_var(userid) ambient\energy\weld1.wav 1
				es_delayed 1.5 es_playsound event_var(attacker) ambient\energy\weld1.wav 1
			}
			es_getplayerlocation wcs_x3 wcs_y3 wcs_z3 event_var(userid)
			es_getplayerlocation wcs_x4 wcs_y4 wcs_z4 event_var(attacker)
			es_delayed 1.5 wcs_spawn event_var(userid) 1
			es_delayed 1.5 wcs_spawn event_var(attacker) 1
			es_delayed 1.5 wcs_teleport event_var(userid) server_var(wcs_x3) server_var(wcs_y3) server_var(wcs_z3)
			es_delayed 1.5 wcs_teleport event_var(attacker) server_var(wcs_x4) server_var(wcs_y4) server_var(wcs_z4)
			es_tell event_var(userid) #multi #green [WCS]#lightgreen Rematch - You called for a rematch
			es_tell event_var(attacker) #multi #green [WCS]#lightgreen Rematch - Panorama called for a rematch
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 3 (END)
	}
}

block zoom
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 4 (START)
	}
	es_playsound server_var(wcs_userid) weapons\zoom.wav .75
	es wcsgroup get panorama_zoom wcs_tmp1 server_var(wcs_userid)
	if (server_var(wcs_tmp1) = 1) do
	{
		es wcsgroup set panorama_zoom server_var(wcs_userid) 2
		es_setplayerprop server_var(wcs_userid) cbaseplayer.m_ifov server_var(wcs_duration)
		es_cexec server_var(wcs_userid) r_screenoverlay sprites/reticle.vmt
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Zoom - You zoomed in
		es_playsound server_var(wcs_userid) items\nvg_on.wav .5
		es_delayed .1 wcs_setcooldown server_var(wcs_userid) 0
	}
	if (server_var(wcs_tmp1) = 2) do
	{
		es wcsgroup set panorama_zoom server_var(wcs_userid) 1
		es_setplayerprop server_var(wcs_userid) cbaseplayer.m_ifov 0
		es_cexec server_var(wcs_userid) r_screenoverlay 0
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Zoom - You zoomed out
		es_playsound server_var(wcs_userid) items\nvg_off.wav .5	
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Panorama - [Block] Skill: 4 (END)
	}
}
