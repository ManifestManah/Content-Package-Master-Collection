/////////////////////////
//Holliday V3 Race Pack//
//Version 1.0.0D       //
/////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;deagle;elite;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;sg552;g3sg1"
		es_delayed .2 es_give event_var(userid) weapon_m249
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;hkp2000;elite;fiveseven;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;ssg08"
		es_delayed .2 es_give event_var(userid) weapon_negev
	}
	ifx false(wcs_falldamage_compensation) do
	{
		es wcsgroup set falldamage event_var(userid) 1
	}
	es wcsgroup set blackout event_var(userid) 1
	es wcsgroup set helicopter event_var(userid) 1
	es wcs_setfx jetpack event_var(userid) = 0 0
	es wcs_setfx speed event_var(userid) = .85 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Death (START)
	}
	ifx false(wcs_falldamage_compensation) do
	{
		es wcsgroup set falldamage event_var(userid) 1
	}
	es wcs_setfx attackspeed event_var(userid) = 1 0
	es wcsgroup set rotor event_var(userid) 0
	es wcsgroup set blackoutjump event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] OnChange (START)
	}
	ifx false(wcs_falldamage_compensation) do
	{
		es wcsgroup set falldamage server_var(wcs_userid) 1
	}
	es wcs_unrestrict server_var(wcs_userid) all
	es wcsgroup set rotor server_var(wcs_userid) 0
	es wcsgroup set blackout server_var(wcs_userid) 0
	es wcsgroup set helicopter server_var(wcs_userid) 0
	es wcsgroup set blackoutjump server_var(wcs_userid) 0
	es wcs_setfx attackspeed server_var(wcs_userid) = 1 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] OnChange (END)
	}
}

event player_footstep
{
	es wcsgroup get blackout wcs_tmp14 event_var(userid)
	if (server_var(wcs_tmp14) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Player_Footstep (START)
		}
		es wcs_shake event_var(userid) 10 13 .3
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es est_effect_08 #a 0 effects/muzzleflashx.vmt server_var(vector1) 2 100 .2 .5 2 400 10 255 255 255 255 10 1
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Player_Footstep (END)
		}
	}
}

block jump
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 1 (START)
	}
	es wcsgroup set blackoutjump event_var(userid) 1
	ifx false(wcs_falldamage_compensation) do
	{
		es_delayed .5 wcsgroup set falldamage event_var(userid) 0
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen High jump - Boosted your jump height and removed fall damage
	}
	ifx true(wcs_falldamage_compensation) do
	{
		es_delayed .5 wcs_setfx health event_var(userid) + 25 0
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen Falldamage Compensation - You've been compensated with 25 extra health
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen High jump - Boosted your jump height
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 1 (END)
	}
}

event player_jump
{
	es wcsgroup get blackoutjump wcs_tmp1 event_var(userid)
	if (server_var(wcs_tmp1) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Player_Jump (START)
		}
		es wcsgroup get helicopter wcs_tmp2 event_var(userid)
		if (server_var(wcs_tmp2) == 1) do
		{
			es wcs_pushed event_var(userid) 0 0 325
		}
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Player_Jump (END)
		}
	}
}

event player_falldamage
{
	es wcsgroup get blackoutjump wcs_tmp1 event_var(userid)
	if (server_var(wcs_tmp1) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Player_Falldamage (START)
		}
		es wcs_shake event_var(userid) 20 20 1
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es est_effect_08 #a 0 effects/muzzleflashx.vmt server_var(vector1) 2 990 2 1 40 400 10 255 255 255 255 10 1
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Player_Falldamage (END)
		}
	}
}

block maximum
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 2 (START)
	}
	es wcs_setfx health event_var(userid) + server_var(wcs_addhealth) 0
	es_delayed 1 wcs_setfx armor event_var(userid) = server_var(wcs_armor) 0
	es_tell event_var(userid) #multi #green [WCS]: #lightgreen Maximum strength - Increased your armor by server_var(wcs_armor) and health by server_var(wcs_addhealth)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 2 (END)
	}
}

block rotor
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			es wcsgroup set rotor event_var(userid) 1
			es_tell event_var(userid) #multi #green [WCS]: #lightgreen Rotor - Increased attack speed on your knife
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 3 (END)
	}
}

event weapon_fire
{
	es wcsgroup get helicopter wcs_tmp2 event_var(userid)
	if (server_var(wcs_tmp2) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Weapon_Fire (START)
		}
		es wcsgroup get rotor wcs_tmp13 event_var(userid)
		if (server_var(wcs_tmp13) == 1) do
		{
			if (event_var(weapon) == weapon_knife) do
			{
				es wcs_setfx attackspeed event_var(userid) = 5 .1
			}
			if (event_var(weapon) == weapon_knife_t) do
			{
				es wcs_setfx attackspeed event_var(userid) = 5 .1
			}
			if (event_var(weapon) == weapon_m249) do
			{
				es wcs_setfx attackspeed event_var(userid) = 1 0
			}
		}
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Weapon_Fire (END)
		}
	}
	if (server_var(wcs_tmp2) == 2) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Weapon_Fire (START)
		}
		es wcsgroup get rotor wcs_tmp13 event_var(userid)
		if (server_var(wcs_tmp13) == 1) do
		{
			if (event_var(weapon) == weapon_knife) do
			{
				es wcs_setfx attackspeed event_var(userid) = 5 .1
			}
			if (event_var(weapon) == weapon_knife_t) do
			{
				es wcs_setfx attackspeed event_var(userid) = 5 .1
			}
		}
		if (event_var(weapon) == weapon_m249) do
		{
			es wcs_setfx attackspeed event_var(userid) = .2 0
		}
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Weapon_Fire (END)
		}
	}
}

block ultimate
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 4 (START)
	}
	es_isbot wcs_tmp1 server_var(wcs_userid)
	if (server_var(wcs_tmp1) = 0) do
	{
		es wcsgroup get helicopter wcs_tmp2 server_var(wcs_userid)
		if (server_var(wcs_tmp2) == 1) do
		{
			es wcsgroup set helicopter server_var(wcs_userid) 2
			es wcs_pushed server_var(wcs_userid) 0 0 300
			es_delayed .3 wcs_setfx jetpack server_var(wcs_userid) = 1 0
			es wcs_setfx attackspeed server_var(wcs_userid) = .2 0
			es wcs_setfx norecoil server_var(wcs_userid) = 1 0
			es_getgame wcs_gamecheck
			if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
			{
				es playerset model server_var(wcs_userid) combine_helicopter
			}
			if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
			{
				es wcs_setmodel server_var(wcs_userid) models/props_vehicles/helicopter_rescue.mdl
			}
		}
		if (server_var(wcs_tmp2) == 2) do
		{
			es wcsgroup set helicopter server_var(wcs_userid) 1
			es wcs_setfx jetpack server_var(wcs_userid) = 0 0
			es wcs_setfx attackspeed server_var(wcs_userid) = 1 0
			es wcs_setfx norecoil server_var(wcs_userid) = 0 0
			ifx true(wcs_ffa_enabled) do
			{
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es playerset model server_var(wcs_userid) player/t_phoenix
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es wcs_setmodel server_var(wcs_userid) 0
				}
			}
			ifx false(wcs_ffa_enabled) do
			{
				es_getplayerteam wcs_team server_var(wcs_userid)
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					if (server_var(wcs_team) = 2) then es playerset model server_var(wcs_userid) player/t_phoenix
					if (server_var(wcs_team) = 3) then es playerset model server_var(wcs_userid) player/ct_urban
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es wcs_setmodel server_var(wcs_userid) 0
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Skill: 4 (END)
	}
}

event bullet_impact
{
	es wcsgroup get helicopter wcs_x11 event_var(userid)
	if (server_var(wcs_x11) == 2) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Bullet_Impact (START)
		}
		es_xrand wcs_dmg 10 25
		ifx true(wcs_ffa_enabled) do
		{
			es_xset wcs_targetteam #alive
		}
		ifx false(wcs_ffa_enabled) do
		{
			es_getplayerteam wcs_team event_var(userid)
			if (server_var(wcs_team) = 2) then es_xset wcs_targetteam #ct
			if (server_var(wcs_team) = 3) then es_xset wcs_targetteam #t
		}
		es wcs_nearcoord wcs_player server_var(wcs_targetteam) event_var(x) event_var(y) event_var(z) 125 "es_xdoblock wcs/tools/holliday/blackout/sound"
		es_playsound event_var(userid) weapons/mortar/mortar_explode2.wav .7
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_xmath wcs_z1 + 20
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es_createvectorstring vector2 event_var(x) event_var(y) event_var(z)	
		es est_effect_06 #a 0 sprites/tp_beam001.vmt server_var(vector2) server_var(vector1) 100 .3 15 9 10 0 255 128 35 255 30
		es est_effect_06 #a 0 sprites/xfireball3.vmt server_var(vector2) server_var(vector1) 100 .3 15 9 10 0 255 255 255 255 30
		es est_effect 11 #a 0 sprites/xfireball3.vmt event_var(x) event_var(y) event_var(z) .7 5 80
		es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector2) 1 10 5 200 10 event_var(x) event_var(y) event_var(z) metal
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Blackout - [Block] Event Bullet_Impact (END)
		}
	}
}

block sound
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Event Bullet_Impact SOUND (START)
	}
	es damage server_var(wcs_player) server_var(wcs_dmg) 1 event_var(userid)
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_playsound server_var(wcs_player) weapons/mortar/mortar_explode2.wav 1
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es_playsound server_var(wcs_player) weapons\hegrenade\explode3.wav 1
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Blackout - [Block] Event Bullet_Impact SOUND (END)
	}
}
