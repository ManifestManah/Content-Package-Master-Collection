////////////////////////
//MiBShidobu Race Pack//
//Version 1.0.1D      //
////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Spawn (START)
	}
	es wcsgroup set cyborg_returnnades event_var(userid) 1
	es_delayed .1 wcs_setcooldown event_var(userid) 5
	es_delayed .5 es_give event_var(userid) weapon_hegrenade
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Death (START)
	}
	es wcsgroup set cyborg_returnnades event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] OnChange (START)
	}
	es wcsgroup set cyborg_returnnades server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] OnChange (END)
	}
}

block health
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 1 (START)
	}
	es wcs_setfx health event_var(userid) + server_var(wcs_addhealth) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Health - You received more health!
	es_playsound event_var(userid) skills/stancehealmonk.mp3 1
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 400 2.5 100 100 0 255 128 128 255 25
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 1 (END)
	}
}

block damage_grenade
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 2 (START)
	}
	if (event_var(weapon) == hegrenade) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_centertell server_var(wcs_userid) Your grenade did server_var(wcs_dmg) extra damage
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_xmath wcs_z1 + 5
				es_delayed .2 est_effect 10 #a 0 sprites\laser.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 0.75 8 5 0 255 71 36 255 0
				es_xmath wcs_z1 + 5
				es_delayed .1  est_effect 10 #a 0 sprites\laser.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 0.75 8 5 0 255 71 36 255 0
				es_xmath wcs_z1 + 5
				es est_effect 10 #a 0 sprites\laser.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 0.75 8 5 0 255 71 36 255 0
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 2 (END)
	}
}

block anti_invisibility
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcsgroup get is_already_colored wcs_tmp6 event_var(userid)
				if (server_var(wcs_tmp6) == 0) do
				{
					es wcsgroup set is_already_colored event_var(userid) 1
					es_delayed 2 es wcsgroup set is_already_colored event_var(userid) 0
					es wcs_getcolors event_var(userid) wcs_red wcs_green wcs_blue wcs_alpha
					ifx true(wcs_ffa_enabled) do
					{
						es_delayed .1 wcs_color event_var(userid) 255 0 0 255 1
					}
					ifx false(wcs_ffa_enabled) do
					{
						if (event_var(es_userteam) = 2) then es_delayed .1 wcs_color event_var(userid) 255 0 0 255 1
						if (event_var(es_userteam) = 3) then es_delayed .1 wcs_color event_var(userid) 0 255 0 255 1
					}
					es_delayed 2 wcs_color event_var(userid) server_var(wcs_red) server_var(wcs_green) server_var(wcs_blue) server_var(wcs_alpha) 1
					es_tell event_var(userid) #multi #green [WCS]#lightgreen Anti-Invisibility - Turned you completely visible!
					es_tell event_var(attacker) #multi #green [WCS]#lightgreen Anti-Invisibility - Made your enemy completely visible!
					es_playsound event_var(userid) wcs/tidetotem.mp3 1
					es_playsound event_var(attacker) wcs/tidetotem.mp3 .8
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 3 (END)
	}
}

block cyborg_ultimate
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice4) <= server_var(wcs_chance)) do
	{
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
		es_xmath wcs_z1 + 35
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector1) 1 10 5 200 10 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) metal
		es_xset wcs_execute_ultimate 0
		ifx true(wcs_ffa_enabled) do
		{
			es_xset wcs_targetteam "#alive"
			es_getlivingplayercount wcs_pcount
			es_set wcs_ffa_userid server_var(wcs_userid)
		}
		ifx false(wcs_ffa_enabled) do
		{
			es_getplayerteam wcs_team server_var(wcs_userid)
			if (server_var(wcs_team) = 2) do
			{
				es_xset wcs_targetteam "#ct,#alive"
				es_getlivingplayercount wcs_pcount 3
			}
			if (server_var(wcs_team) = 3) do
			{
				es_xset wcs_targetteam "#t,#alive"
				es_getlivingplayercount wcs_pcount 2
			}
		}
		if (server_var(wcs_targetteam) != 0) do
		{	
			if (server_var(wcs_pcount) > 0) do
			{
				es_xset wcs_counter 0
				es_xset wcs_initial_players_struck 0
				es_xset wcs_count_players_ultimate_immunity 0
				es_xset wcs_count_players_actually_affected 0
				es_xset wcs_magnitude 125
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
				es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 225 "es_xdoblock wcs/tools/manifest/cyborg/wcs_ulti_suicide_ultimate_checks"
				if (server_var(wcs_execute_ultimate) == 1) do
				{
					es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen The Explosion affected server_var(wcs_count_players_actually_affected) enemies
					es wcs_centertell server_var(wcs_userid) The Explosion dealt 125 damage to server_var(wcs_count_players_actually_affected) enemies
					es_playsound server_var(wcs_userid) weapons/hegrenade/explode4.wav .7
					es_delayed .2 es_playsound server_var(wcs_userid) weapons/hegrenade/explode4.wav .6
					es_delayed .3 es_playsound server_var(wcs_userid) weapons/hegrenade/explode4.wav .7
					es damage server_var(wcs_userid) 1500 1 server_var(wcs_userid)
				}
			}
			else do
			{
				es_xset wcs_ultinotexec 1
				es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, there are no living enemies!
			}
			if (server_var(wcs_counter) = 0) do
			{
				es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, no nearby enemies!
				es_xset wcs_ultinotexec 1
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Could not target an enemy team
		}
		if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	}
	if (server_var(wcs_dice4) > server_var(wcs_chance)) do
	{
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Overloading yourself failed!
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 4 (END)
	}
}

block wcs_ulti_suicide_ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 4 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es_xmath wcs_counter + 1
				if (server_var(wcs_counter) > 0) do
				{
					if (server_var(wcs_counter) <= 20) do
					{
						es_set wcs_initial_players_struck server_var(wcs_counter)
						es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
						if (server_var(wcs_tmp) != 1) do
						{
							es_xset wcs_ultinotexec 0
							es_xset wcs_execute_ultimate 1
							es_xmath wcs_count_players_actually_affected + 1
							es_xdoblock wcs/tools/manifest/cyborg/wcs_ulti_suicide_ultimate_final
						}
						if (server_var(wcs_tmp) == 1) do
						{
							es_xmath wcs_count_players_ultimate_immunity + 1
							if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's ultimate
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
							}
							if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, all nearby enemy targets were immune!
								es_xset wcs_ultinotexec 1
							}
						}
					}
					else do
					{
						es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you can't hit that many targets at once!
						es_xset wcs_ultinotexec 1
					}
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 4 (CHECKS END)
	}
}

block wcs_ulti_suicide_ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 4 (EXECUTED START)
	}
	es wcs_centertell server_var(wcs_targetid) You took server_var(wcs_magnitude) damage from someone's Wrath
	es_xrand wcs_rand 1 3
	if (server_var(wcs_rand) == 1) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode5.wav .7
	}
	if (server_var(wcs_rand) == 2) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode4.wav .8
	}
	if (server_var(wcs_rand) == 3) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode3.wav .7
	}
	es damage server_var(wcs_targetid) 125 1 server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Cyborg - [Block] Skill: 4 (EXECUTED END)
	}
}

event hegrenade_detonate
{
	es wcsgroup get cyborg_returnnades wcs_tmp event_var(userid)
	if (server_var(wcs_tmp) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Cyborg - [Block] Event Hegrenade_Detonate (START)
		}
		es_delayed .5 es_give event_var(userid) weapon_hegrenade
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Cyborg - [Block] Event Hegrenade_Detonate (END)
		}
	}
}