//////////////////////
//Kamiqawa Race Pack//
//Version 1.0.1D    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;deagle;elite;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;aug;sg550;awp;sg552;g3sg1;m249"
		es_delayed .1 es_give event_var(userid) weapon_scout
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Weaponry - You received a Scout and you cannot use other firearms.
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;hkp2000;elite;fiveseven;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;m249;negev"
		es_delayed .1 es_give event_var(userid) weapon_ssg08
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Weaponry - You received a SSG and you cannot use other firearms.
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 20
	es est_effect 11 #a 0 sprites/plasma1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 1 3 150
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector1) 50 150 1 1 8 10 0 255 255 255 180 2 5
	es_xmath wcs_z1 + 20
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector1) 50 150 1 1 8 10 0 255 255 255 180 2 5
	es_xmath wcs_z1 + 20
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector1) 50 150 1 1 8 10 0 255 255 255 180 2 5
	es_xmath wcs_z1 + 20
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector1) 50 150 1 1 8 10 0 255 255 255 180 2 5
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Spawn (END)
	}
}

block damage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_xrand wcs_dmg 5 20
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_centertell event_var(attacker) Punish dealt + server_var(wcs_dmg) damage
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z1 + 20
				es_xmath wcs_z2 + 20
				es est_effect 11 #a 0 sprites/plasma1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 2 4 155
				es est_effect 3 #a 0 sprites/tp_beam001.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 2 50 50 255 255 255 255
				es est_effect 3 #a 0 sprites/strider_blackball.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 2 30 30 255 255 255 255
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 1 (END)
	}
}

block longjump
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 2 (START)
	}
	es wcs_setfx longjump event_var(userid) + server_var(wcs_multiplier) 0
	es_xmath wcs_multiplier * 100
	es_xmath wcs_multiplier - 100
	es wcs_decimal wcs_multiplier server_var(wcs_multiplier)
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Gap closer - Increased your jump length by server_var(wcs_multiplier)%
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 2 (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 3 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_xmath wcs_speed * 100
	es_xmath wcs_speed - 100
	es wcs_decimal wcs_speed server_var(wcs_speed)
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Movement speed - You gained server_var(wcs_speed)% speed
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 3 (END)
	}
}

block gravity
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 4 (START)
	}
	es_delayed .2 wcs_setfx gravity event_var(userid) = server_var(wcs_gravity) 0
	es_xmath wcs_gravity * 100
	es wcs_decimal wcs_gravity server_var(wcs_gravity)
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Packed lightly - Gravity reduced to server_var(wcs_gravity)%
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 4 (END)
	}
}

block teleport
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 5 (START)
	}
    es_xset wcs_ok 1
    es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
    es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es_xmath wcs_z2 + 5
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector2) 50 250 1 1 8 10 0 255 255 255 180 2 5
	es_delayed .2 est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector2) 50 250 1 .8 8 10 0 255 255 255 180 2 5
	es_xmath wcs_z2 + 20
	es est_effect 11 #a 0 sprites/plasma1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 1.5 140
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector2) 50 150 1 1 8 10 0 255 255 255 180 2 5
	es_delayed .2 est_effect_08 #a 0 sprites/tp_beam001.vmt server_var(vector2) 50 150 1 .8 8 10 0 255 255 255 180 2 5
	es_xmath wcs_z2 - 25

    es_xcopy wcs_x wcs_x1
    es_xcopy wcs_y wcs_y1
    es_xcopy wcs_z wcs_z1

    es_math wcs_x - server_var(wcs_x2)
    es_math wcs_y - server_var(wcs_y2)
    es_math wcs_z - server_var(wcs_z2)

    if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
    if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
    if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1

    if (server_var(wcs_range) < server_var(wcs_x)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_y)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_z)) do
    {
		es_xset wcs_ok 0
    }
    
    ifx true(wcs_ok) do
    {
 	    ifx true(wcs_legacy_teleport_fade) do
	    {
	    	es wcs_fade server_var(wcs_userid) 65 255 30 50 1 1
	    }
 	    ifx true(wcs_legacy_teleport_type) do
	    {
	    	//To Do: Improve this by finding an alternative teleportation method that prevent players from getting stuck in displacemnts and models
	    	es playerset location server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    	es_delayed .01 wcs_teleport server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    }
	    ifx false(wcs_legacy_teleport_type) do
	    {
	    	es_setplayerprop server_var(wcs_userid) CBasePlayer.localdata.m_vecBaseVelocity 0,0,250
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) 2
	    }
        es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenTeleport - You Teleported!
        es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
    }
    
    else do
    {
        if (server_var(wcs_range) < server_var(wcs_x)) do
        {
            if (server_var(wcs_x2) >= 0) do
            {
                if (server_var(wcs_x1) >= 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_x1) < 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_x1 wcs_x2
        }
        
        if (server_var(wcs_range) < server_var(wcs_y)) do
        {
            if (server_var(wcs_y2) >= 0) do
            {
                if (server_var(wcs_y1) >= 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_y1) < 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_y1 wcs_y2
        }
        
        if (server_var(wcs_range) < server_var(wcs_z)) do
        {
            if (server_var(wcs_z2) >= 0) do
            {
                if (server_var(wcs_z1) >= 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_z1) < 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_z1 wcs_z2
        }

	    ifx false(wcs_legacy_teleport_type) do
	    {
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) .1
	    }
 	    ifx true(wcs_legacy_teleport_fade) do
	    {
	    	es wcs_fade server_var(wcs_userid) 65 255 30 50 1 1
	    }
        es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenTeleport - You cannot teleport that far!
	    es wcs_setcooldown server_var(wcs_userid) 0
    }
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Scout Punisher - [Block] Skill: 5 (END)
	}
}
