////////////////////////////
//TeacherCreatur Race Pack//
//Version 1.0.0D          //
////////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Spawn (START)
	}
	es_fire event_var(userid) !self addoutput "renderfx 13"
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 11 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 2 1.5 100
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Spawn (END)
	}
}

block shocker
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcs_shake event_var(userid) 33 20 0.8
				es wcs_pushed event_var(userid) 0 0 625
				es wcsgroup set is_frozen event_var(userid) 1
				es_delayed 1.5 wcsgroup set is_frozen event_var(userid) 0
				es_delayed .5 es wcs_setfx freeze event_var(userid) = 1 .2
				es_delayed 1 wcs_pushed event_var(userid) 0 230 0
				es_delayed 1.3 es wcs_setfx freeze event_var(userid) = 1 .2
				es_delayed 1.3 es wcs_pushed event_var(userid) 100 0 0
				es wcs_centertell event_var(userid) You've got shocked by event_var(es_attackername) and went numb!
				es wcs_centertell event_var(attacker) Shocker made event_var(es_username) go numb!
				es_playsound event_var(userid) ambient\levels\citadel\weapon_disintegrate1.wav 1.0
				es_playsound event_var(attacker) ambient\levels\citadel\weapon_disintegrate1.wav 1.0
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
				es_xmath wcs_z1 + 20
				es_xmath wcs_z2 + 20
				es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 15 25 255 255 255 255
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 1 (END)
	}
}

block bounce
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 2 (START)
	}
	es wcs_setfx longjump event_var(userid) + server_var(wcs_multiplier)
	es_delayed .2 wcs_setfx gravity event_var(userid) = server_var(wcs_gravity) 0
	es_tell event_var(userid) #multi #green [WCS]: #lightgreen Electricity bounce reduced your gravity and increased your jump lenght! 
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 60 61 2 60 100 0.8 0 20 100 255 1
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 2 (END)
	}
}

block unstable
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(userid) ambient\explosions\explode_7.wav .9
					es_playsound event_var(attacker) ambient\explosions\explode_7.wav .6
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(userid) weapons\hegrenade\explode3.wav .8
					es_playsound event_var(attacker) weapons\hegrenade\explode3.wav .5
				}
				es wcs_pushed event_var(attacker) 0 0 80
				es_delayed .2 wcs_pushed event_var(attacker) 100 0 0
				es_tell event_var(attacker) #multi #green [WCS]: #lightgreen event_var(es_username)'s Unstable electric armor electrified you!
				es_tell event_var(userid) #multi #green [WCS]: #lightgreen Unstable electric armor electrified event_var(es_attackername)!
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_xmath wcs_z1 + 40
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 230 130 1 128 100 0 155 115 100 200 5
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 3 (END)
	}
}

block strike
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 4 (START)
	}
	es_xset wcs_execute_ultimate 0
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 300 "es_xdoblock wcs/tools/manifest/mr_electric/ultimate_checks"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				//everything related to the wcs_userid
				es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Lightning Strike struck server_var(wcs_count_players_actually_affected) enemies
				es wcs_centertell server_var(wcs_userid) Your Lightning Strike dealt server_var(wcs_dmg) damage
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es playsound server_var(wcs_userid) ambient\levels\citadel\weapon_disintegrate1.wav 1.0
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es playsound server_var(wcs_userid) ambient\energy\weld2.wav 1.0
				}	
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, you need to aim at an enemy
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 4 (END)
	}
}

block ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 4 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es_xmath wcs_counter + 1
				if (server_var(wcs_counter) > 0) do
				{
					if (server_var(wcs_counter) <= 20) do
					{
						es_set wcs_initial_players_struck server_var(wcs_counter)
						es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
						if (server_var(wcs_tmp) != 1) do
						{
							es_xset wcs_ultinotexec 0
							es_xset wcs_execute_ultimate 1
							es_xmath wcs_count_players_actually_affected + 1
							es_xdoblock wcs/tools/manifest/mr_electric/ultimate_final
						}
						if (server_var(wcs_tmp) == 1) do
						{
							es_xmath wcs_count_players_ultimate_immunity + 1
							if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_targetid) #multi #green [WCS]: #lightgreen Ultimate immunity - You blocked an enemy's ultimate
								es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
							}
							if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, all nearby enemy targets were immune!
								es_xset wcs_ultinotexec 1
							}
						}
					}
					else do
					{
						es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, you can't hit that many targets at once!
						es_xset wcs_ultinotexec 1
					}
				}
			}
		}	
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 4 (CHECKS END)
	}
}

block ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 4 (EXECUTED START)
	}
	es wcs_centertell server_var(wcs_targetid) You took server_var(wcs_dmg) damage from Mr. Electric's lightning strike
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_targetid)
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 75 150 1 60 10 0.8 155 115 100 200 1
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 75 150 1 60 10 0.8 155 115 100 200 1
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es playsound server_var(wcs_targetid) ambient\levels\citadel\weapon_disintegrate1.wav 1.0
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es playsound server_var(wcs_targetid) ambient\energy\weld2.wav 1.0
	}
	es damage server_var(wcs_targetid) server_var(wcs_dmg) 1 server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Mr. Electric - [Block] Skill: 4 (EXECUTED END)
	}
}