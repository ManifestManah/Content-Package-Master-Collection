////////////////////////////////
//Various Authors V4 Race Pack//
//Version 1.1.0A              //
////////////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Spawn (START)
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 40
	es est_effect 10 #a 0 effects/fire_cloud1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 100 750 2 60 10 0 255 0 0 200 4
	es_xmath wcs_z1 + 40
	es est_effect 10 #a 0 effects/fire_cloud1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 100 750 2 60 10 0 255 0 0 200 2
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Spawn (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 1 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_delayed .2 wcs_setfx gravity event_var(userid) = server_var(wcs_gravity) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Unholy Aura - Increased your speed by server_var(wcs_speed)%
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 1 (END)
	}
}

block damage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcsgroup get electricsmokerdmg wcs_y1 event_var(userid)
				if (server_var(wcs_y1) == 0) do
				{
					if (event_var(weapon) != hegrenade) do
					{
						es wcsgroup set electricsmokerdmg event_var(userid) 1
						es_delayed 3 wcsgroup set electricsmokerdmg event_var(userid) 0
						es_delayed 1 damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
						es_delayed 2 damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
						es_delayed 3 damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
						es wcs_centertell event_var(attacker) Dealing server_var(wcs_dmg) damage per second for 3 seconds
						es wcs_centertell event_var(userid) Took + server_var(wcs_dmg) damage per second for 3 seconds
						es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
						es_xmath wcs_z1 + 15
						es est_effect 10 #a 0 sprites/flatflame.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 200 2 10 120 10 255 255 0 255 1
						es_xmath wcs_z1 + 45
						es est_effect 10 #a 0 sprites/flatflame.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 200 2 10 120 10 255 255 0 255 1
					}
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 2 (END)
	}
}

block thunder
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_xrand wcs_rand 1 2
				es wcs_drop event_var(userid) server_var(wcs_rand)
				if (server_var(wcs_rand) == 1) then es_tell event_var(userid) #multi #green [WCS]#lightgreen Thunder wave - Caused you to drop your primary weapon
				if (server_var(wcs_rand) == 2) then es_tell event_var(userid) #multi #green [WCS]#lightgreen Thunder wave - Caused you to drop your secondary weapon
				if (server_var(wcs_rand) == 1) then es_tell event_var(attacker) #multi #green [WCS]#lightgreen Thunder wave - Made your enemy drop his primary weapon
				if (server_var(wcs_rand) == 2) then es_tell event_var(attacker) #multi #green [WCS]#lightgreen Thunder wave - Made your enemy drop his secondary weapon
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
				es_xmath wcs_z1 + 30
				es_xmath wcs_z2 + 30
				es est_effect 3 #a 0 sprites/richo1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 10 10 255 162 0 255
				es est_effect 3 #a 0 sprites/flatflame.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 25 25 255 255 255 255
				es_xmath wcs_z1 - 10
				es est_effect 10 #a 0 sprites/richo1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 175 1 25 10 5 255 255 0 255 3
				es_xmath wcs_z1 + 20
				es est_effect 10 #a 0 sprites/flatflame.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 175 1 25 10 5 255 255 0 255 3
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 3 (END)
	}
}

block teleport
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 4 (START)
	}
    es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
    es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es est_effect 7 #a 0 sprites/smoke.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 40 1
	es_xmath wcs_z2 + 30
	es est_effect 10 #a 0 sprites/smoke.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 50 250 .5 60 10 2 255 255 0 255 5
	es_xmath wcs_z2 - 30
	ifx true(wcs_legacy_teleport_fade) do
	{
		es wcs_fade event_var(userid) 65 255 30 50 1 1
	}
	ifx true(wcs_legacy_teleport_type) do
	{
		es playerget viewcoord server_var(wcs_userid) wcs_x2 wcs_y2 wcs_z2
		es playerset location server_var(wcs_userid) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		es wcs_getdistance wcs_distance server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		if (server_var(wcs_tmp3) > server_var(wcs_range)) do
		{
	        es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenMax Distance - You cannot travel that far!
		    es wcs_setcooldown server_var(wcs_userid) 0
		}
		else 
		{
			es_delayed .01 wcs_teleport server_var(wcs_userid) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
			es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenSmoke Teleportation - You Teleported!
		}
	}
	ifx false(wcs_legacy_teleport_type) do
	{
		es wcs_teleport_push server_var(wcs_userid) server_var(wcs_range)
		es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenSmoke Teleportation - You Teleported!
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Electric Smoker - [Block] Skill: 4 (END)
	}
}
