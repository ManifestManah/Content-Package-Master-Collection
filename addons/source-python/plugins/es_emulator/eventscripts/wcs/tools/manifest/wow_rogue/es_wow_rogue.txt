//////////////////////
//Kamiqawa Race Pack//
//Version 1.1.0A    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Spawn (START)
	}
	es wcsgroup set is_attackspeed_altered event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Spawn (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] OnChange (START)
	}
	es wcsgroup set is_attackspeed_altered server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] OnChange (END)
	}
}

block steal
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_xrand wcs_money 25 400
				es playerget cash wcs_tmp1 event_var(userid)
				if (server_var(wcs_tmp1) < server_var(wcs_money)) do
				{
					es_tell event_var(userid) #multi #green [WCS]#lightgreen Pick pocket - Failed, the target doesn't have enough cash
				}
				if (server_var(wcs_tmp1) >= server_var(wcs_money)) do
				{
					es wcs_setfx cash event_var(userid) - server_var(wcs_money) 0
					es wcs_setfx cash event_var(attacker) + server_var(wcs_money) 0
					es_tell event_var(attacker) #multi #green [WCS]#lightgreen Pick pocket - You stole server_var(wcs_money) from event_var(es_username)
					es_tell event_var(userid) #multi #green [WCS]#lightgreen Pick pocket - A rogue stole server_var(wcs_money)$ from you
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 1 (END)
	}
}

block attackspeed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 2 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		es wcsgroup set is_attackspeed_altered event_var(userid) 1
		es wcs_setfx attackspeed event_var(userid) = server_var(wcs_speed) 0
		es_xmath wcs_speed * 100
		es_xmath wcs_speed - 100
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Dual wielding - Attack speed increased by server_var(wcs_speed)%
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 2 (END)
	}
}

block invis
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 3 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		es_delayed .5 wcs_color event_var(userid) 255 255 255 server_var(wcs_invis) 0
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Invisibility - Made you less visible
		es_playsound event_var(userid) wcs/invisibility.mp3 .8
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 3 (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice4) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcs_setfx speed event_var(userid) + .75 3
				es wcs_centertell event_var(userid) Gained 75% speed from sprint for 3 seconds
				es wcs_centertell event_var(attacker) The rogue used sprint, hurry!
				es_playsound event_var(userid) wcs/speed.mp3 .7
				es_playsound event_var(attacker) wcs/speed.mp3 .9
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_xmath wcs_z1 + 5
				es est_effect 10 #a 0 sprites/glow1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 40 80 .75 12 10 0 255 205 10 190 2
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 40 80 .75 12 10 0 210 50 15 175 7
				es_xmath wcs_z1 + 15
				es_delayed .1 est_effect 10 #a 0 sprites/glow1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 40 80 .75 12 10 0 255 205 10 190 2
				es_delayed .1 est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 40 80 .75 12 10 0 210 50 15 175 7
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 4 (END)
	}
}

block smokebomb
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 5 (START)
	}
	es_xset wcs_execute_ultimate 0
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 325 "es_xdoblock wcs/tools/manifest/wow_rogue/ultimate_checks"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Smoke Bomb - Created a distrction and dealt 15 damage
				es wcs_centertell server_var(wcs_userid) You distracted server_var(wcs_counter) enemies with a smokebomb!
				es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 75 74 1.5 30 10 0 170 170 170 200 1
				es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 105 104 1.5 30 10 0 170 170 170 200 1
				es_xmath wcs_z1 - 10
				es_format coords "origin %1 %2 %3" server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es_xmath wcs_smokestack_counter + 1
				es_give server_var(wcs_userid) env_smokestack
				es_format ss_name "my_ss_%1%2" server_var(wcs_userid) server_var(wcs_smokestack_counter)
				es_entitysetvalue server_var(eventscripts_lastgive) "targetname" server_var(ss_name)
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "basespread 20"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "spreadspeed 125"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "speed 40"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "rate 15"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "startsize 20"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "endsize 45"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "twist 0"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "jetlength 60"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "angles 0 0 0"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput server_var(coords)
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "rendercolor 140 140 140"
				es_fire server_var(wcs_userid) server_var(ss_name) addoutput "SmokeMaterial particle/fire.vmt"
				es_fire server_var(wcs_userid) server_var(ss_name) turnon
				es_delayed 1.5 es_fire server_var(wcs_userid) server_var(ss_name) kill
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, no nearby enemies!
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 5 (END)
	}
}

block ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 5 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es_xmath wcs_counter + 1
				if (server_var(wcs_counter) > 0) do
				{
					if (server_var(wcs_counter) <= 20) do
					{
						es_set wcs_initial_players_struck server_var(wcs_counter)
						es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
						if (server_var(wcs_tmp) != 1) do
						{
							es_xset wcs_ultinotexec 0
							es_xset wcs_execute_ultimate 1
							es_xmath wcs_count_players_actually_affected + 1
							es_xdoblock wcs/tools/manifest/wow_rogue/ultimate_final
						}
						if (server_var(wcs_tmp) == 1) do
						{
							es_xmath wcs_count_players_ultimate_immunity + 1
							if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's ultimate
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
							}
							if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, all nearby enemy targets were immune!
								es_xset wcs_ultinotexec 1
							}
						}
					}
					else do
					{
						es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you can't hit that many targets at once!
						es_xset wcs_ultinotexec 1
					}
				}
			}
		}	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 5 (CHECKS END)
	}
}

block ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 5 (EXECUTED START)
	}
	es wcs_centertell server_var(wcs_targetid) You were distracted by a smokebomb!
	es damage server_var(wcs_targetid) 25 1 server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: WoW - Rogue - [Block] Skill: 5 (EXECUTED END)
	}
}