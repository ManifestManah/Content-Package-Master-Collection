//////////////////////
//Builders Race Pack//
//Version 1.1.0A    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Spawn (START)
	}
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es playerset model event_var(userid) models/props_c17/oildrum001
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_setmodelprop event_var(userid) models/props\coop_cementplant/exloding_barrel/exploding_barrel.mdl
	}
	es_xrand wcs_tmp2 2 4
	es_delayed .4 es_give event_var(userid) weapon_hegrenade
	es_delayed .5 es_setplayerprop event_var(userid) "CCSPlayer.baseclass.m_iAmmo.014" server_var(wcs_tmp2)
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Bag of hand grenades - Gave you server_var(wcs_tmp2) high explosive grenades
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Appearence - You look like an explosive barrel
	es wcsgroup set bconstruction event_var(userid) 15
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 375 376 3 150 10 0 0 0 255 200 10
	es est_effect 10 #a 0 sprites/plasma.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 375 376 3 150 10 0 0 0 125 200 10
	es_xmath wcs_z1 + 25
	es est_effect 10 #a 0 sprites/plasma.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 51 3 11 11 0 0 0 255 255 1
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/plasma.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 51 3 6 6 0 0 0 255 255 2
	es_xmath wcs_z1 + 15		
	es est_effect 10 #a 0 sprites/plasma.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 51 3 4 4 0 0 0 255 255 3
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Death (START)
	}
	es wcsgroup set bconstruction event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] OnChange (START)
	}
	es wcsgroup set bconstruction server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] OnChange (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 1 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_xmath wcs_speed * 100
	es_xmath wcs_speed - 100
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Speed - Increased your speed by server_var(wcs_speed)%
	es_delayed 1.5 es_playsound event_var(userid) wcs/speed.mp3 .8
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 1 (END)
	}
}

block health
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 2 (START)
	}
	es_delayed .2 wcs_setfx health event_var(userid) + server_var(wcs_addhealth) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Health - You received more health!
	es_playsound event_var(userid) skills/stancehealmonk.mp3 1
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 400 2.5 100 100 0 255 128 128 255 25
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 2 (END)
	}
}

block longjump
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 3 (START)
	}
	es wcs_setfx longjump event_var(userid) + server_var(wcs_multiplier) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Longjump - You have increased jump length
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 3 (END)
	}
}

block respawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice4) <= server_var(wcs_chance)) do 
	{
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Reincarnation - Respawning where you died in 3 seconds!
		es wcs_centertell event_var(userid) Respawning where you died in 3
		es_delayed 1 es wcs_centertell event_var(userid) Respawning where you died in 2
		es_delayed 2 es wcs_centertell event_var(userid) Respawning where you died in 1
		es_delayed 3 es wcs_centertell event_var(userid) You've been respawned!
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_delayed 3 es_emitsound player event_var(userid) wcs/resurrect.mp3 1.0 0.3
		es_delayed 3 wcs_spawn event_var(userid) 1
		es_delayed 3.1 wcs_teleport event_var(userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 130 131 3 80 20 0 255 71 36 200 10
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 4 (END)
	}
}

block build
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 5 (START)
	}
	es wcsgroup get bconstruction wcs_tmp11 server_var(wcs_userid)
	if (server_var(wcs_tmp11) == 0) do
	{
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Construction - You don't have any props left
	}
	if (server_var(wcs_tmp11) => 1) do
	{
		es_xmath wcs_tmp11 - 1
		es wcsgroup set bconstruction server_var(wcs_userid) server_var(wcs_tmp11)
		es_getgame wcs_gamecheck
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_prop_physics_create server_var(wcs_userid) props_c17/oildrum001_explosive.mdl
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es wcs_prop_explosive_barrel server_var(wcs_userid)
		}
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Construction - server_var(wcs_tmp11) props left
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: MadCan - Builder Race - [Block] Skill: 5 (END)
	}
}