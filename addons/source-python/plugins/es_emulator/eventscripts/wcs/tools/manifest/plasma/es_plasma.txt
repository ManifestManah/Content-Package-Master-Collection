////////////////////////////////
//Various Authors V3 Race Pack//
//Version 1.1.0A              //
////////////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Spawn (START)
	}
	es wcsgroup set evasion event_var(userid) 0
	es wcsgroup set evasion_chance event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Death (START)
	}
	es wcsgroup set evasion event_var(userid) 0
	es wcsgroup set evasion_chance event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] OnChange (START)
	}
	es wcsgroup set evasion server_var(wcs_userid) 0
	es wcsgroup set evasion_chance server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] OnChange (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcs_setfx speed event_var(userid) = 1.75 1
				es wcs_color event_var(userid) 255 255 255 0 0
				es_delayed 1 wcs_color event_var(userid) 255 255 255 255 0
				es wcs_centertell event_var(userid) Transform made you invisible and increased your speed for 1 second
				es_playsound event_var(userid) ambient/energy/zap9.wav .6
				es_playsound event_var(attacker) ambient/energy/zap9.wav .8
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_xmath wcs_z1 + 4
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 8 10 .5 8 8 0 0 162 232 210 1
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 8 100 .5 8 8 0 0 162 232 210 2
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 8 200 .5 8 8 0 0 162 232 210 3
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 1 (END)
	}
}

block headshot
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			es wcs_setfx headshotimmunity event_var(userid) = 1 0
			es_tell event_var(userid) #multi #green [WCS]#lightgreen Shock helm - Renders you immune to headshots
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 2 (END)
	}
}

block dissolve
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_attackerdead) = 0) do
		{
			es_xrand wcs_tmp1 0 1
			if (server_var(wcs_tmp1) == 1) do
			{
				 es wcs_removeweapon event_var(userid) 1
				 es_tell event_var(userid) #multi #green [WCS]#lightgreen Dissolver - Dissolved event_var(es_attackername)'s primary weapon
				 es_tell event_var(attacker) #multi #green [WCS]#lightgreen Dissolver - Your primary weapon got dissolved
			}
			else do
			{
				es wcs_removeweapon event_var(userid) 2
				es_tell event_var(userid) #multi #green [WCS]#lightgreen Dissolver - Dissolved event_var(es_attackername)'s secondary weapon
				es_tell event_var(attacker) #multi #green [WCS]#lightgreen Dissolver - Your secondary weapon got dissolved
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 3 (END)
	}
}

block shield
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 4 (START)
	}
	es_xrand wcs_rand 50 75
	es wcsgroup set evasion server_var(wcs_userid) 1
	es wcsgroup set evasion_chance server_var(wcs_userid) server_var(wcs_rand)
	es wcsgroup set evasion_count server_var(wcs_userid) 0
	es_delayed server_var(wcs_duration) wcsgroup get evasion_count wcs_tmp1 server_var(wcs_userid)
	es_delayed server_var(wcs_duration) es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Plasma shield - You evaded server_var(wcs_tmp1) attacks
	es_delayed server_var(wcs_duration) wcsgroup set evasion server_var(wcs_userid) 0
	es_delayed server_var(wcs_duration) wcsgroup set evasion_chance server_var(wcs_userid) 0
	es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Plasma shield - server_var(wcs_rand)% chance to evade for server_var(wcs_duration) seconds
	es wcs_centertell server_var(wcs_userid) Increased your chance to evade for server_var(wcs_duration) seconds!
	es_playsound server_var(wcs_userid) ambient/energy/force_field_loop1.wav .8
	es_delayed server_var(wcs_duration) es_stopsound server_var(wcs_userid) ambient/energy/force_field_loop1.wav
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_xmath wcs_z1 + 15
	es est_effect 11 #a 0 sprites/strider_blackball.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_duration) 1.5 255
	es_xmath wcs_duration - 1
	es_delayed server_var(wcs_duration) es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Plasma shield - Your evasion is fading...
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Plasma - [Block] Skill: 4 (END)
	}
}