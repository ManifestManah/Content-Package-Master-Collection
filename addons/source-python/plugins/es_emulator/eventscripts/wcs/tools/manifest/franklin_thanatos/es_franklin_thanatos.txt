/////////////////////////////
//Various Authors Race Pack//
//Version 1.0.0D           //
/////////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;elite;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;sg552;g3sg1;m249"
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;hkp2000;elite;fiveseven;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;ssg08;m249;negev"
	}
	es wcs_setfx speed event_var(userid) = 0.9 0
	es wcs_setfx health event_var(userid) = 75 0
	es_delayed .1 es_give event_var(userid) weapon_knife
	es_delayed .1 es_give event_var(userid) weapon_deagle
	es_tell event_var(userid) #multi #green [WCS]: #lightgreen Franklin Thanatos - You received your golden deagle.
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Death (START)
	}
	es wcsgroup get medalofhonor wcs_tmp3 event_var(userid)
	if (server_var(wcs_tmp3) == 1) do
	{
		es wcsgroup set medalofhonor event_var(userid) 0
		es_tell event_var(userid) #multi #green [WCS]: #lightgreen Medal of Honor - Respawning where you died in 3 seconds!
		es wcs_centertell event_var(userid) Respawning where you died in 3
		es_delayed 1 es wcs_centertell event_var(userid) Respawning where you died in 2
		es_delayed 2 es wcs_centertell event_var(userid) Respawning where you died in 1
		es_delayed 3 es wcs_centertell event_var(userid) You've been respawned with more HP!
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_delayed 3 es_emitsound player event_var(userid) wcs/resurrect.mp3 1.0 0.3
		es_delayed 3 wcs_spawn event_var(userid) 1
		es_delayed 3.1 wcs_teleport event_var(userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es_delayed 3.6 wcs_setfx speed event_var(userid) + .25 0
		es_delayed 3.6 wcs_setfx health event_var(userid) + 25 0
		es_delayed 3.6 wcs_setfx gravity event_var(userid) - .1 0
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 130 131 3 80 20 0 255 71 36 200 10
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Death (END)
	}
}

block medalofhonor
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 1 (START)
	}
	es wcsgroup set medalofhonor event_var(userid) 0
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		ifx true(wcs_ffa_enabled) do
		{
			es_tell event_var(userid) #multi #green [WCS]: #lightgreen Medal of Honor - If you die during this round you'll be brought back to life!
			es wcsgroup set medalofhonor event_var(userid) 1
		}
		ifx false(wcs_ffa_enabled) do
		{
			es wcsgroup get has_used_phoenix wcs_tmp3 event_var(userid)
			if (server_var(wcs_tmp3) == 0) do
			{
				es wcsgroup set has_used_phoenix event_var(userid) 1
				if (server_var(wcs_dice) <= server_var(wcs_chance)) do
				{
					if (event_var(es_userteam) = 2) then es_xset wcs_team #t
					if (event_var(es_userteam) = 3) then es_xset wcs_team #ct
					es wcsgroup get phoenix wcs_phoenix server_var(wcs_team)
					es_xmath wcs_phoenix + 1
					es wcsgroup set phoenix server_var(wcs_team) server_var(wcs_phoenix)
					es_tell event_var(userid) #multi #green [WCS]: #lightgreen Medal of Honor - The first from your team to die will be revived
					es wcsgroup set medalofhonor event_var(userid) 1
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 1 (END)
	}
}

block golden_deagle
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_attackerweapon) == weapon_deagle) do
		{
			if (event_var(es_userdead) = 0) do
			{
				if (event_var(es_attackerdead) = 0) do
				{
					es_xrand wcs_dmg 150 225
					es wcs_setfx 2ndclip event_var(userid) - 3 0
					es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
					es wcs_centertell event_var(attacker) Your golden deagle dealt server_var(wcs_dmg) bonus damage
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 2 (END)
	}
}

block laststand
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 3 (START)
	}
	if (event_var(es_attackerdead) = 0) do
	{
		es_tell event_var(attacker) #multi #green [WCS]: #lightgreen Last Stand - You gained some speed, but lost some health
		es playerget health wcs_tmp3 event_var(attacker)
		if (server_var(wcs_tmp3) > server_var(wcs_tmp1)) do
		{
			es wcs_setfx health event_var(attacker) - server_var(wcs_tmp1) 0
			es wcs_setfx speed event_var(attacker) + server_var(wcs_tmp2) 0
		}
		if (server_var(wcs_tmp3) <= server_var(wcs_tmp1)) do
		{
			es wcs_setfx health event_var(attacker) = 1 0
			es wcs_setfx speed event_var(attacker) + server_var(wcs_tmp2) 0
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 3 (END)
	}
}

block bomb_teamhealth
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (START)
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_xmath wcs_z1 + 35
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector1) 1 10 5 200 10 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) metal
	es_xset wcs_execute_ultimate 0
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius) "es_xdoblock wcs/tools/manifest/franklin_thanatos/wcs_ulti_suicide_thanatos_checks"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen The explosion affected server_var(wcs_count_players_actually_affected) enemies
				es wcs_centertell server_var(wcs_userid) You sacrificed yourself for your comrades
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_emitsound player server_var(wcs_userid) ambient/levels/canals/windchime2.wav 1 .4
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es_playsound server_var(wcs_userid) ambient\misc\windchime2.wav .8
				}
				ifx true(wcs_ffa_enabled) do
				{
					es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Sacrifice - Franklin Thanatos died in vain...
				}
				ifx false(wcs_ffa_enabled) do
				{
					es_getplayerteam wcs_team server_var(wcs_userid)
					if (server_var(wcs_team) = 2) do
					{
						wcs_foreach player wcs_targetid #t "es_xdoblock wcs/tools/manifest/franklin_thanatos/bomb_teamhealth_exe"
					}
					if (server_var(wcs_team) = 3) do
					{
						wcs_foreach player wcs_targetid #ct "es_xdoblock wcs/tools/manifest/franklin_thanatos/bomb_teamhealth_exe"
					}
				}
				es damage server_var(wcs_userid) 1500 1 server_var(wcs_userid)



			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, no nearby enemies!
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (END)
	}
}

block wcs_ulti_suicide_thanatos_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (CHECKS START)
	}
	es_exists ex userid server_var(wcs_targetid)
	if (server_var(ex) = 1) do
	{
		es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
		if (server_var(wcs_alive) = 0) do
		{
			es_xmath wcs_counter + 1
			if (server_var(wcs_counter) > 0) do
			{
				if (server_var(wcs_counter) <= 20) do
				{
					es_set wcs_initial_players_struck server_var(wcs_counter)
					es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
					if (server_var(wcs_tmp) != 1) do
					{
						es_xset wcs_ultinotexec 0
						es_xset wcs_execute_ultimate 1
						es_xmath wcs_count_players_actually_affected + 1
						es_xdoblock wcs/tools/manifest/franklin_thanatos/bomb_teamhealth_final
					}
					if (server_var(wcs_tmp) == 1) do
					{
						es_xmath wcs_count_players_ultimate_immunity + 1
						if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_targetid) #multi #green [WCS]: #lightgreen Ultimate immunity - You blocked an enemy's ultimate
							es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
						}
						if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, all nearby enemy targets were immune!
							es_xset wcs_ultinotexec 1
						}
					}
				}
				else do
				{
					es_tell server_var(wcs_userid) #multi #green [WCS]: #lightgreen Ultimate - Failed, you can't hit that many targets at once!
					es_xset wcs_ultinotexec 1
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (CHECKS END)
	}
}

block bomb_teamhealth_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (EXECUTED START)
	}
	es wcs_centertell server_var(wcs_targetid) You took server_var(wcs_magnitude) damage from an explosion
	es_xrand wcs_rand 1 3
	if (server_var(wcs_rand) == 1) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode5.wav .7
	}
	if (server_var(wcs_rand) == 2) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode4.wav .8
	}
	if (server_var(wcs_rand) == 3) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode3.wav .7
	}
	es damage server_var(wcs_targetid) server_var(wcs_magnitude) 1 server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (EXECUTED END)
	}
}

block bomb_teamhealth_exe
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (BOMB_TEAMHEALTH_EXE EXECUTED START)
	}
	es wcs_setfx health server_var(wcs_targetid) + server_var(wcs_addhealth) 0
	es_tell server_var(wcs_targetid) #multi #green [WCS]: #lightgreen Sacrifice - Franklin Thanatos healed you for server_var(wcs_addhealth) health
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Franklin Thanatos - [Block] Skill: 4 (BOMB_TEAMHEALTH_EXE EXECUTED END)
	}
}