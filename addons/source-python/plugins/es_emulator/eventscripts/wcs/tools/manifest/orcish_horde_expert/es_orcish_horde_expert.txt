////////////////////
//Expert Race Pack//
//Version 1.0.1D  //
////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Spawn (START)
	}
	es_delayed .1 es_give event_var(userid) weapon_hegrenade
	es_xmath wcs_smokestack_counter + 1
	es_give event_var(userid) point_tesla
	es_format ss_name "my_ss_%1%2" event_var(userid) server_var(wcs_smokestack_counter)
	es_entitysetvalue server_var(eventscripts_lastgive) "targetname" server_var(ss_name)
	es_fire event_var(userid) server_var(ss_name) addoutput "m_Color 252 70 35"
	es_fire event_var(userid) server_var(ss_name) addoutput "m_flRadius 580"
	es_fire event_var(userid) server_var(ss_name) addoutput "beamcount_min 1250"
	es_fire event_var(userid) server_var(ss_name) addoutput "beamcount_max 5250"
	es_fire event_var(userid) server_var(ss_name) addoutput "thick_min 9"
	es_fire event_var(userid) server_var(ss_name) addoutput "thick_max 6"
	es_fire event_var(userid) server_var(ss_name) addoutput "lifetime_min .1"
	es_fire event_var(userid) server_var(ss_name) addoutput "lifetime_max .3"
	es_fire event_var(userid) server_var(ss_name) addoutput "interval_min .1"
	es_fire event_var(userid) server_var(ss_name) addoutput "interval_max .2"
	es_fire event_var(userid) server_var(ss_name) addoutput "texture sprites/lgtning.vmt"
	es_fire event_var(userid) server_var(ss_name) DoSpark
	es_delayed .2 es_fire event_var(userid) server_var(ss_name) DoSpark
	es_delayed .4 es_fire event_var(userid) server_var(ss_name) DoSpark
	es_delayed .2 es_fire event_var(userid) server_var(ss_name) DoSpark
	es_delayed .6 es_fire event_var(userid) server_var(ss_name) DoSpark
	es_delayed .8 es_fire event_var(userid) server_var(ss_name) DoSpark
	es_delayed 1 es_fire event_var(userid) server_var(ss_name) DoSpark
	es_delayed 1.5 es_fire event_var(userid) server_var(ss_name) kill
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Spawn (END)
	}
}

block strike
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= 10) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do 
			{
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_centertell event_var(attacker) Critical strike did + server_var(wcs_dmg) damage!
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z1 + 20
				es_xmath wcs_z2 + 20
				es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 8 8 255 71 36 255
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 1 (END)
	}
}			

block grenade
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 2 (START)
	}
	if (event_var(weapon) == hegrenade) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es_tell event_var(attacker) #multi #green [WCS]#lightgreen Critical grenade - Dealt#green server_var(wcs_dmg) #lightgreenadditional damage
				es_tell event_var(userid) #multi #green [WCS]#lightgreen Critical grenade - Recieved#green server_var(wcs_dmg) #lightgreenadditional damage
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_xmath wcs_z1 + 15
				es est_effect 10 #a 0 sprites\lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 0.75 8 5 0 255 71 36 255 0
				es_xmath wcs_z1 + 20
				es est_effect 10 #a 0 sprites\lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 0.75 8 5 0 255 71 36 255 0
				es_xmath wcs_z1 + 20
				es est_effect 10 #a 0 sprites\lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 0.75 8 5 0 255 71 36 255 0
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 2 (END)
	}
}

block respawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 1) do
		{
			es_tell event_var(userid) #multi #green [WCS]#lightgreen Reincarnation - Respawning in #green3 #lightgreenseconds!
			es wcs_centertell event_var(userid) Respawning in 3 seconds!
			es_delayed 1 es wcs_centertell event_var(userid) Respawning in 2 seconds!
			es_delayed 2 es wcs_centertell event_var(userid) Respawning in 1 seconds!
			es_delayed 3 es wcs_centertell event_var(userid) You've been respawned! 
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
			es_delayed 3 es_emitsound player event_var(userid) wcs/resurrect.mp3 1.0 0.3
			es_delayed 3 wcs_spawn event_var(userid) 1
			es_delayed 3.1 wcs_teleport event_var(userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
			es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 130 131 3 80 20 0 255 71 36 200 10
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 3 (END)
	}
}

block wcs_ulti_chain
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 4 (START)
	}
	es_xset wcs_execute_ultimate 0
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius) "es_xdoblock wcs/tools/manifest/orcish_horde_expert/ultimate_checks_chain"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				es_playsound server_var(wcs_userid) wcs/lightning.mp3 .7
				es wcs_centertell server_var(wcs_userid) You've struck server_var(wcs_count_players_actually_affected) enemies with your chain lightning
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, no nearby enemies!
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 4 (END)
	}
}

block ultimate_checks_chain
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 4 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es_xmath wcs_counter + 1
				if (server_var(wcs_counter) > 0) do
				{
					if (server_var(wcs_counter) <= 20) do
					{
						es_set wcs_initial_players_struck server_var(wcs_counter)
						es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
						if (server_var(wcs_tmp) != 1) do
						{
							es_xset wcs_ultinotexec 0
							es_xset wcs_execute_ultimate 1
							es_xmath wcs_count_players_actually_affected + 1
							es_xdoblock wcs/tools/manifest/orcish_horde_expert/ultimate_final_chain
						}
						if (server_var(wcs_tmp) == 1) do
						{
							es_xmath wcs_count_players_ultimate_immunity + 1
							if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's ultimate
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
							}
							if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, all nearby enemy targets were immune!
								es_xset wcs_ultinotexec 1
							}
						}
					}
					else do
					{
						es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you can't hit that many targets at once!
						es_xset wcs_ultinotexec 1
					}
				}
			}
		}	
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 4 (CHECKS END)
	}
}

block ultimate_final_chain
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 4 (EXECUTED START)
	}
	es_playsound server_var(wcs_targetid) wcs/lightning.mp3 .8
	es wcs_centertell server_var(wcs_targetid) You've been struck by a chain lightning
	es_xmath wcs_smokestack_counter + 1
	es_give server_var(wcs_targetid) point_tesla
	es_format ss_name "my_ss_%1%2" server_var(wcs_targetid) server_var(wcs_smokestack_counter)
	es_entitysetvalue server_var(eventscripts_lastgive) "targetname" server_var(ss_name)
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "m_Color 170 232 255"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "m_flRadius server_var(wcs_radius)"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "beamcount_min 1450"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "beamcount_max 1750"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "thick_min 7"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "thick_max 3"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "lifetime_min .1"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "lifetime_max .3"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "interval_min .1"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "interval_max .2"
	es_fire server_var(wcs_targetid) server_var(ss_name) addoutput "texture sprites/lgtning.vmt"
	es_fire server_var(wcs_targetid) server_var(ss_name) DoSpark
	es_delayed .2 es_fire server_var(wcs_targetid) server_var(ss_name) DoSpark
	es_delayed .4 es_fire server_var(wcs_targetid) server_var(ss_name) DoSpark
	es_delayed .7 es_fire server_var(wcs_targetid) server_var(ss_name) kill
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid)
	es_xmath wcs_z1 + 38
	es_xmath wcs_z2 + 38
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector1) server_var(vector2) 10 .2 10 1 1 13 160 160 230 255 11
	es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector1) server_var(vector2) 10 .2 10 2 2 12 150 150 255 220 8
	es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector1) server_var(vector2) 10 .2 10 1 1 14 200 200 255 235 10
	es damage server_var(wcs_targetid) 35 1 server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Orcish Horde Expert - [Block] Skill: 4 (EXECUTED END)
	}
}