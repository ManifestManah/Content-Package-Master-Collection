//////////////////////
//Manifest Race Pack//
//Version 1.0.1B    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Spawn (START)
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 5
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es est_effect_08 #a 0 sprites/hydraspinalcord.vmt server_var(vector1) 350 125 1 .5 100 10 .1 5 120 5 120 18 5
	es est_effect_08 #a 0 sprites/hydraspinalcord.vmt server_var(vector1) 425 125 1 1 100 10 .1 5 120 5 120 15 5
	es wcsgroup set is_attackspeed_altered event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Death (START)
	}
	es wcsgroup set is_attackspeed_altered event_var(userid) 0
	es wcsgroup set skeletonexplosive event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] OnChange (START)
	}
	es wcsgroup set is_attackspeed_altered server_var(wcs_userid) 0
	es wcsgroup set skeletonexplosive server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] OnChange (END)
	}
}

block explosive
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 1 (START)
	}
	es wcsgroup set skeletonexplosive event_var(userid) 1
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Explosive arrow - Chance to deal aoe damage
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 1 (END)
	}
}

event bullet_impact
{
	es wcsgroup get skeletonexplosive wcs_tmp12 event_var(userid)
	if (server_var(wcs_tmp12) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Event Bullet_Impact (START)
		}
		es_xrand wcs_rand 1 100
		if (server_var(wcs_rand) <= 5) do
		{
			es_getgame wcs_gamecheck
			if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
			{
				es_xset wcs_tmp4 weapons/shotgun/shotgun_dbl_fire.wav
				es_playsound event_var(userid) weapons/shotgun/shotgun_dbl_fire.wav .6
			}
			if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
			{
				es_xset wcs_tmp4 weapons\hegrenade\explode4.wav
				es_playsound event_var(userid) weapons\hegrenade\explode4.wav 1
			}
			es wcs_centertell event_var(userid) You fired an explosive arrow
			es_getplayerteam wcs_team event_var(userid)
			if (server_var(wcs_team) = 2) then es_xset wcs_targetteam #ct
			if (server_var(wcs_team) = 3) then es_xset wcs_targetteam #t
			es wcs_nearcoord wcs_player server_var(wcs_targetteam) event_var(x) event_var(y) event_var(z) 125 "es damage server_var(wcs_player) 15 1 event_var(userid);es_playsound server_var(wcs_player) server_var(wcs_tmp4) 1"
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
			es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
			es_createvectorstring vector2 event_var(x) event_var(y) event_var(z)
			es est_effect_15 #a 0 effects/fire_embers3.vmt server_var(vector1) server_var(vector2) 3 100 1
			es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector2) 1 10 5 200 10 event_var(x) event_var(y) event_var(z) metal
		}
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Event Bullet_Impact (END)
		}
	}
}

block archery
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 2 (START)
	}
	es wcs_setfx attackspeed event_var(userid) =  server_var(wcs_speed) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Archery - Your attackspeed has been increased
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 2 (END)
	}
}

block distance
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 3 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_delayed .2 wcs_setfx gravity event_var(userid) = server_var(wcs_multiplier) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Keeping distance - Increased your speed and jump height
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 3 (END)
	}
}

block paraarrow
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 4 (START)
	}
	es_xset wcs_execute_ultimate 0
	es_getplayerteam wcs_team server_var(wcs_userid)
	if (server_var(wcs_team) = 2) do
	{
		es_xset wcs_targetteam "#ct,#alive"
		es_getlivingplayercount wcs_pcount 3
	}
	if (server_var(wcs_team) = 3) do
	{
		es_xset wcs_targetteam "#t,#alive"
		es_getlivingplayercount wcs_pcount 2
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 150 "es_xdoblock wcs/tools/manifest/manrik/ultimate_checks"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				//everything related to the wcs_userid

				es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Paralysing Arrow affected server_var(wcs_count_players_actually_affected) people
				es wcs_centertell server_var(wcs_userid) Paralyzing arrow paralyzed server_var(wcs_count_players_actually_affected) enemies
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you need to aim at an enemy
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 4 (END)
	}
}

block ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 4 (CHECKS START)
	}
	es_exists ex userid server_var(wcs_targetid)
	if (server_var(ex) = 1) do
	{
		es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
		if (server_var(wcs_alive) = 0) do
		{
			es_xmath wcs_counter + 1
			if (server_var(wcs_counter) > 0) do
			{
				if (server_var(wcs_counter) <= 20) do
				{
					es_set wcs_initial_players_struck server_var(wcs_counter)
					es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
					if (server_var(wcs_tmp) != 1) do
					{
						es_xset wcs_ultinotexec 0
						es_xset wcs_execute_ultimate 1
						es_xmath wcs_count_players_actually_affected + 1
						es_xdoblock wcs/tools/manifest/manrik/ultimate_final
					}
					if (server_var(wcs_tmp) == 1) do
					{
						es_xmath wcs_count_players_ultimate_immunity + 1
						if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's ultimate
							es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
						}
						if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, all nearby enemy targets were immune!
							es_xset wcs_ultinotexec 1
						}
					}
				}
				else do
				{
					es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you can't hit that many targets at once!
					es_xset wcs_ultinotexec 1
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 4 (CHECKS END)
	}
}

block ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 4 (EXECUTED START)
	}
	es wcsgroup get is_paralyzed wcs_tmp3 server_var(wcs_targetid)
	if (server_var(wcs_tmp3) == 0) do
	{
		es wcsgroup set is_paralyzed server_var(wcs_targetid) 1
		es_delayed .75 wcsgroup set is_paralyzed server_var(wcs_targetid) 0
		es wcs_centertell server_var(wcs_targetid) You were struck by a paralysing arrow
		es wcs_setfx paralyze server_var(wcs_targetid) = 1 0.75
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_targetid)
		es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid)
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		es est_effect_15 #a 0 effects/blueflare1.vmt server_var(vector1) server_var(vector2) 3 100 1
		es est_effect_15 #a 0 effects/yellowflare_noz.vmt server_var(vector1) server_var(vector2) 3 100 1
		es_xmath wcs_z2 + 30
		es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
		es est_effect_08 #a 0 effects/blueflare1.vmt server_var(vector2) 20 275 1 0.5 60 10 0 0 162 232 180 4 1
		es est_effect_08 #a 0 effects/yellowflare_noz.vmt server_var(vector2) 20 275 1 0.5 85 10 0 0 162 232 220 5 1
	}
	if (server_var(wcs_tmp3) == 1) do
	{
		es wcs_centertell server_var(wcs_userid) Your target was immune to paralyzing effects!
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Manrik - The Skeleton Archer - [Block] Skill: 4 (EXECUTED END)
	}
}
