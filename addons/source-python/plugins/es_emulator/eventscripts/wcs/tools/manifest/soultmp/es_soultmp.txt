//////////////////////
//Kamiqawa Race Pack//
//Version 1.0.1D    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;deagle;elite;fiveseven;m3;xm1014;mac10;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;sg552;g3sg1;m249"
		es_delayed .1 es_give event_var(userid) weapon_tmp
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Weaponry - You received a TMP and you cannot use other firearms.
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;hkp2000;elite;fiveseven;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;ssg08;m249;negev"
		es_delayed .1 es_give event_var(userid) weapon_mp7
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Weaponry - You received a Mp7 and you cannot use other firearms.
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 10 #a 0 sprites/tp_beam001.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 100 120 1 60 10 8 170 102 51 75 3
	es_xmath wcs_z1 + 25
	es est_effect 11 #a 0 sprites/strider_blackball.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 1 2 75
	es est_effect 11 #a 0 sprites/plasma1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 1 5 75
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Spawn (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 1 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_xmath wcs_speed * 100
	es_xmath wcs_speed - 100
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Move quickly - Increased your speed by server_var(wcs_speed)%
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 1 (END)
	}
}

block invis
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 2 (START)
	}
	es_delayed .5 wcs_color event_var(userid) 255 255 255 server_var(wcs_invis) 0
 	es_xmath wcs_invis / -2.55
	es_xmath wcs_invis + 100
    es wcs_decimal wcs_invis server_var(wcs_invis)
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Stealth - You've become server_var(wcs_invis)% invisible!
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 2 (END)
	}
}

block leech
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do 
			{
				es_xrand wcs_dmg 3 8
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z1 + 20
				es_xmath wcs_z2 + 20
				es est_effect 3 #a 0 sprites/tp_beam001.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 10 10 170 102 51 115
				es est_effect 11 #a 0 sprites/strider_blackball.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 1 2 75
				es est_effect 11 #a 0 sprites/plasma1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 1 5 75
				es playerget health wcs_tmp2 event_var(attacker)
				if (server_var(wcs_tmp2) >= 300) do
				{
					es_xrand wcs_addhealth 6 23
					es wcs_setfx health event_var(attacker) + server_var(wcs_addhealth) 0
					es wcs_centertell event_var(attacker) Soul transfer did + server_var(wcs_dmg) damage and stole server_var(wcs_addhealth) health!
				}
				if (server_var(wcs_tmp2) < 300) do
				{
					es wcs_setfx health event_var(attacker) = 300 0
					es wcs_centertell event_var(attacker) You have reached the 300 health cap!
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 3 (END)
	}
}

block teleport
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 4 (START)
	}
    es_xset wcs_ok 1

    es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
    es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es est_effect 10 #a 0 sprites/tp_beam001.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 100 120 1 60 10 8 170 102 51 75 3
	es_xmath wcs_z2 + 25
	es est_effect 11 #a 0 sprites/plasma1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 5 75
    es_xmath wcs_z2 - 25

    es_xcopy wcs_x wcs_x1
    es_xcopy wcs_y wcs_y1
    es_xcopy wcs_z wcs_z1

    es_math wcs_x - server_var(wcs_x2)
    es_math wcs_y - server_var(wcs_y2)
    es_math wcs_z - server_var(wcs_z2)

    if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
    if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
    if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1

    if (server_var(wcs_range) < server_var(wcs_x)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_y)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_z)) do
    {
		es_xset wcs_ok 0
    }
    
    ifx true(wcs_ok) do
    {
 	    ifx true(wcs_legacy_teleport_fade) do
	    {
	    	es wcs_fade server_var(wcs_userid) 65 255 30 50 1 1
	    }
 	    ifx true(wcs_legacy_teleport_type) do
	    {
	    	//To Do: Improve this by finding an alternative teleportation method that prevent players from getting stuck in displacemnts and models
	    	es playerset location server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    	es_delayed .01 wcs_teleport server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    }
	    ifx false(wcs_legacy_teleport_type) do
	    {
	    	es_setplayerprop server_var(wcs_userid) CBasePlayer.localdata.m_vecBaseVelocity 0,0,250
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) 2
	    }
        es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenTeleport - You Teleported!
        es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
    }
    
    else do
    {
        if (server_var(wcs_range) < server_var(wcs_x)) do
        {
            if (server_var(wcs_x2) >= 0) do
            {
                if (server_var(wcs_x1) >= 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_x1) < 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_x1 wcs_x2
        }
        
        if (server_var(wcs_range) < server_var(wcs_y)) do
        {
            if (server_var(wcs_y2) >= 0) do
            {
                if (server_var(wcs_y1) >= 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_y1) < 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_y1 wcs_y2
        }
        
        if (server_var(wcs_range) < server_var(wcs_z)) do
        {
            if (server_var(wcs_z2) >= 0) do
            {
                if (server_var(wcs_z1) >= 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_z1) < 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_z1 wcs_z2
        }

	    ifx false(wcs_legacy_teleport_type) do
	    {
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) .1
	    }
 	    ifx true(wcs_legacy_teleport_fade) do
	    {
	    	es wcs_fade server_var(wcs_userid) 65 255 30 50 1 1
	    }
        es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenTeleport - You cannot teleport that far!
	    es wcs_setcooldown server_var(wcs_userid) 0
    }
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Soul - [Block] Skill: 4 (END)
	}
}
