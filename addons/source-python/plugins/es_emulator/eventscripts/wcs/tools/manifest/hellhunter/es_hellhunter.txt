block spawn
{
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;deagle;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;sg552;g3sg1;m249"
		es_playsound event_var(userid) ambient\atmosphere\hole_hit2.wav 1
		es_playsound event_var(userid) ambient/atmosphere/city_skypass1.wav 1
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;hkp2000;fiveseven;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;ssg08;m249;negev"
	}
	es_delayed .5 es_give event_var(userid) weapon_elite
    es_xmath wcs_smokestack_counter + 1
	es_give event_var(userid) env_smokestack
	es_format ss_name "my_ss_%1%2" event_var(userid) server_var(wcs_smokestack_counter)
    es_entitysetvalue server_var(eventscripts_lastgive) "targetname" server_var(ss_name)
    es_fire event_var(userid) server_var(ss_name) addoutput "basespread 15"
    es_fire event_var(userid) server_var(ss_name) addoutput "spreadspeed 100"
    es_fire event_var(userid) server_var(ss_name) addoutput "speed 50"
    es_fire event_var(userid) server_var(ss_name) addoutput "rate 200"
    es_fire event_var(userid) server_var(ss_name) addoutput "startsize 15"
    es_fire event_var(userid) server_var(ss_name) addoutput "endsize 25"
    es_fire event_var(userid) server_var(ss_name) addoutput "twist 5"
    es_fire event_var(userid) server_var(ss_name) addoutput "jetlength 500"
	es_fire event_var(userid) server_var(ss_name) addoutput "angles 90 90 90"
    es_fire event_var(userid) server_var(ss_name) addoutput "rendercolor 220 100 110"
    es_fire event_var(userid) server_var(ss_name) addoutput "SmokeMaterial particle/fire.vmt"
	es_fire event_var(userid) server_var(ss_name) turnon
    es_delayed 6 es_fire event_var(userid) server_var(ss_name) kill
    es_xmath wcs_smokestack_counter + 1
	es_give event_var(userid) point_tesla
	es_format ss_name "my_ss_%1%2" event_var(userid) server_var(wcs_smokestack_counter)
    es_entitysetvalue server_var(eventscripts_lastgive) "targetname" server_var(ss_name)
	es_fire event_var(userid) server_var(ss_name) addoutput "m_color 255 25 25"
	es_fire event_var(userid) server_var(ss_name) addoutput "m_flradius 75"
	es_fire event_var(userid) server_var(ss_name) addoutput "beamcount_min 30"
	es_fire event_var(userid) server_var(ss_name) addoutput "beamcount_max 60"
	es_fire event_var(userid) server_var(ss_name) addoutput "thick_min 5"
	es_fire event_var(userid) server_var(ss_name) addoutput "thick_max 30"
	es_fire event_var(userid) server_var(ss_name) addoutput "lifetime_min 1"
	es_fire event_var(userid) server_var(ss_name) addoutput "lifetime_max 2"
	es_fire event_var(userid) server_var(ss_name) addoutput "interval_min 15"
	es_fire event_var(userid) server_var(ss_name) addoutput "interval_max 30"
	es_fire event_var(userid) server_var(ss_name) dospark
	es_delayed 3 es_fire event_var(userid) server_var(ss_name) kill
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Death (START)
	}
	es wcsgroup set dualrapidfire event_var(userid) 0
	es wcsgroup set hellhunter_flashbangs event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] OnChange (START)
	}
	es wcsgroup set dualrapidfire server_var(wcs_userid) 0
	es wcsgroup set hellhunter_flashbangs server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] OnChange (END)
	}
}

block firearrows
{
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			es_getgame wcs_gamecheck
			if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
			{
				es_playsound event_var(userid) ambient\fire\mtov_flame2.wav 1
				es_playsound event_var(attacker) weapons/fx/nearmiss/bulletltor05.wav 1
				es_delayed .1 es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor03.wav 1
				es_delayed .3 es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor04.wav 1
			}
			if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
			{
				es_playsound event_var(attacker) weapons/fx/nearmiss/bulletltor06.wav 1
				es_delayed .1 es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor08.wav 1
				es_delayed .3 es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor07.wav 1
			}
			es wcs_shake event_var(userid) 15 15 2
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
			es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
			es_xmath wcs_z1 + 50
			es_xmath wcs_z2 + 50
			es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
			es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
			es est_effect_06 #a 0 sprites/orangelight1_v2.vmt server_var(vector1) server_var(vector2) 10 .3 1 1 1 0 255 205 45 255 100
			es est_effect 11 #a 0 sprites/fire1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) .5 .5 255
			es_xmath wcs_y2 + 80
			es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
			es_delayed .4 est_effect_06 #a 0 sprites/orangelight1_v2.vmt server_var(vector1) server_var(vector2) 10 .3 1 1 1 0 255 205 45 255 100
			es_delayed .4 est_effect 11 #a 0 sprites/fire1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) .5 .5 255
			es_xmath wcs_y2 - 160
			es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
			es_delayed .5 est_effect_06 #a 0 sprites/orangelight1_v2.vmt server_var(vector2) server_var(vector1) 100 .3 1 1 10 0 255 205 25 255 100
			es_delayed .5 est_effect 11 #a 0 sprites/fire1.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) .5 .5 255
		}
	}
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 1 (END)
	}
}

block teleflashbangs
{
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 2 (START)
	}
	es wcsgroup set hellhunter_flashbangs event_var(userid) server_var(wcs_ammo)
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Teleport flashbangs - You've received server_var(wcs_ammo) flashbangs
	es_delayed .1 es_give event_var(userid) weapon_flashbang
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 2 (END)
	}
}

event flashbang_detonate
{
	es wcsgroup get hellhunter_flashbangs wcs_z10 event_var(userid)
	if (server_var(wcs_z10) >= 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			es_log [Race]: Hell Hunter - [Block] Event Flashbang_Detonate (START)
		}
		es_delayed .05 wcs_teleport event_var(userid) event_var(x) event_var(y) event_var(z)
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Teleport flashbangs - You've teleported
		es_give event_var(userid) weapon_flashbang
		ifx true(wcs_race_debugging) do
		{
			es_log [Race]: Hell Hunter - [Block] Event Flashbang_Detonate (END)
		}
	}
}

block antifalldamage
{
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			ifx false(wcs_falldamage_compensation) do
			{
				es wcs_setfx falldamage event_var(userid) = 1.0 0
				es_tell event_var(userid) #multi #green [WCS]#lightgreen Anti-falldamage - You wont take damage when falling
			}
			ifx true(wcs_falldamage_compensation) do
			{
				es_delayed .5 wcs_setfx health event_var(userid) + 25 0
				es_tell event_var(userid) #multi #green [WCS]#lightgreen Falldamage Compensation - You've been compensated with 25 extra health
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 3 (END)
	}
}

block dualrapidfire
{
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 4 (START)
	}
	es wcsgroup set dualrapidfire server_var(wcs_userid) 1
	es_delayed server_var(wcs_duration) es wcsgroup set dualrapidfire server_var(wcs_userid) 0
	es wcs_setfx attackspeed server_var(wcs_userid) = 4 server_var(wcs_duration)
	es wcs_centertell server_var(wcs_userid) Dual rapid fire
	es_delayed server_var(wcs_duration) es wcs_centertell server_var(wcs_userid) No more rapid fire
	es wcsgroup get hellhunter_flashbangs wcs_z10 server_var(wcs_userid)
	if (server_var(wcs_z10) <= 2) do
	{
		es_xmath wcs_z10 + 2
		es_give server_var(wcs_userid) weapon_flashbang
		es wcsgroup set hellhunter_flashbangs server_var(wcs_userid) server_var(wcs_z10)
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Dual rapid fire - Granted you two teleport flashbang
	}
	ifx true(wcs_race_debugging) do
	{
		es_log [Race]: Hell Hunter - [Block] Skill: 4 (END)
	}
}

event weapon_fire
{
	es wcsgroup get dualrapidfire wcs_tmp6 event_var(userid)
	if (server_var(wcs_tmp6) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			es_log [Race]: Hell Hunter - [Block] Event Weapon_Fire (START)
		}
		if (event_var(weapon) == weapon_elite) do
		{
			es playerget weaponindex wcs_tmp9 event_var(userid) weapon_elite
			es_setindexprop server_var(wcs_tmp9) "cbasecombatweapon.localactiveweapondata.m_flnextprimaryattack" 0			
			es_setplayerprop event_var(userid) "ccsplayer.baseclass.baseclass.bcc_localdata.m_flnextattack" 1
			es_setplayerprop event_var(userid) "CCSPlayer.cslocaldata.m_iShotsFired" -1
			es_playsound event_var(userid) weapons/elite/elite-1.wav 1
		}
		ifx true(wcs_race_debugging) do
		{
			es_log [Race]: Hell Hunter - [Block] Event Weapon_Fire (END)
		}
	}
}

event bullet_impact
{
	es wcsgroup get dualrapidfire wcs_tmp6 event_var(userid)
	if (server_var(wcs_tmp6) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			es_log [Race]: Hell Hunter - [Block] Event Bullet_Impact (START)
		}
		es est_effect 11 #a 0 sprites/fire1.vmt event_var(x) event_var(y) event_var(z) .5 .5 255
		es_xrand wcs_rand 1 2
		if (server_var(wcs_rand) == 1) then es wcs_setfx 2ndclip event_var(userid) = 30 0
		if (server_var(wcs_rand) == 2) then es wcs_setfx 2ndclip event_var(userid) = 29 0
		ifx true(wcs_race_debugging) do
		{
			es_log [Race]: Hell Hunter - [Block] Event Bullet_Impact (END)
		}
	}
}
