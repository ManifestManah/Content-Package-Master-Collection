////////////////////////////////
//Various Authors V3 Race Pack//
//Version 1.0.0D              //
////////////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;deagle;elite;fiveseven;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;sg552;g3sg1;m249"
		es_delayed .5 es_give event_var(userid) weapon_m3
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Weaponry - You're limited to m3, grenades and knife!
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;hkp2000;elite;fiveseven;deagle;revolver;tec9;taser;sawedoff;xm1014;mag7;p90;cz75a;mp9;mp7;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;ssg08;m249;negev"
		es_delayed .5 es_give event_var(userid) weapon_nova
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Weaponry - You're limited to nova, grenades and knife!
	}
	es wcs_setfx jetpack event_var(userid) = 0 0
	es wcsgroup set lordofgrav_fly event_var(userid) 1
	es_delayed .5 wcs_color event_var(userid) 255 128 64 255 1
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 15
	es est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 1.5 50 20 5 51 51 204 255 2
	es_xmath wcs_z1 + 10
	es est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 51 1.5 50 20 5 51 51 204 255 4
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 75 76 1.5 50 20 5 51 51 204 255 8
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 51 1.5 50 20 2.5 51 51 204 255 4
	es_xmath wcs_z1 + 10
	es est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 26 1.5 50 20 0 51 51 204 255 2
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Spawn (END)
	}
}

block planet
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcs_pushed event_var(userid) 0 0 500
				es_delayed .3 wcs_setfx gravity event_var(userid) = 2 1
				es wcs_centertell event_var(userid) event_var(es_attackername)'s gravitational wobble struck you!
				es wcs_centertell event_var(attacker) Gravitational wobble struck event_var(es_username)!
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_xmath wcs_z1 + 25
				es est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 45 46 1 15 25 0 255 127 39 255 2
				es_xmath wcs_z1 + 15
				es est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 45 46 1 15 25 0 255 127 39 255 4
				es_delayed .3 est_effect 10 #a 0 sprites/bluelight1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 150 0.75 30 25 0 255 127 39 255 4
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 1 (END)
	}
}

block weight
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 2 (START)
	}
	es_delayed .2 wcs_setfx gravity event_var(userid) = server_var(wcs_gravity) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Reduced weight - allows you to jump higher!
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 2 (END)
	}
}

block hide
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcsgroup get lordofgrav_hide wcs_tmp2 event_var(userid)
				if (server_var(wcs_tmp2) == 0) do
				{
					es wcsgroup set lordofgrav_hide event_var(userid) 1
					es_delayed 2 wcsgroup set lordofgrav_hide event_var(userid) 0
					es wcs_color event_var(userid) 255 255 255 0 1
					es wcs_centertell event_var(userid) You've hidden yourself - for 2 seconds!
					es_delayed 2 wcs_color event_var(userid) 255 128 64 255 1
					es_delayed 2 es wcs_centertell event_var(userid) You're no longer invisible!
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 3 (END)
	}
}

block liftoff
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 4 (START)
	}
	es_isbot wcs_tmp1 server_var(wcs_userid)
	if (server_var(wcs_tmp1) = 0) do
	{
		es wcsgroup get lordofgrav_fly wcs_tmp2 server_var(wcs_userid)
		if (server_var(wcs_tmp2) == 1) do
		{
			es wcsgroup set lordofgrav_fly server_var(wcs_userid) 2
			es wcs_setfx health server_var(wcs_userid) + server_var(wcs_addhealth) 0
			es wcs_setfx jetpack server_var(wcs_userid) = 1 0
			es wcs_centertell server_var(wcs_userid) You are now flying
			es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
			es_xmath wcs_z2 + 20
			es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 5 55 1 10 10 0 97 197 222 165 1
			es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 5 60 1 10 10 0 153 217 234 200 1
		}
		if (server_var(wcs_tmp2) == 2) do
		{
			es wcsgroup set lordofgrav_fly server_var(wcs_userid) 1
			es wcs_setfx jetpack server_var(wcs_userid) = 0 0
			es wcs_centertell server_var(wcs_userid) You are no longer flying
			es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
			es_xmath wcs_z2 + 20
			es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 55 5 1 10 10 0 97 197 222 165 1
			es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 60 5 1 10 10 0 153 217 234 200 1
			es playerget health wcs_tmp1 server_var(wcs_userid)
			if (server_var(wcs_tmp1) >= server_var(wcs_addhealth)) do
			{
				es wcs_setfx health server_var(wcs_userid) - server_var(wcs_addhealth) 0
			}
			if (server_var(wcs_tmp1) < server_var(wcs_addhealth)) do
			{
				es wcs_setfx health server_var(wcs_userid) = 1 0
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Lord Of Gravity - [Block] Skill: 4 (END)
	}
}
