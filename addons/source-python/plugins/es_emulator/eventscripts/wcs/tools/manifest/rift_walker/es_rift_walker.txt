////////////////////
//Naiken Race Pack//
//Version 1.1.0A  //
////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Spawn (START)
	}
	es wcs_removeweapon event_var(userid) 1
	es wcs_removeweapon event_var(userid) 2
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp;p228;deagle;elite;fiveseven;m3;xm1014;mac10;tmp;mp5navy;ump45;p90;galil;famas;ak47;m4a1;scout;aug;sg550;awp;sg552;g3sg1;m249"
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es wcs_restrict event_var(userid) "glock;usp_silencer;p250;cz75a;hkp2000;elite;fiveseven;deagle;revolver;tec9;taser;sawedoff;nova;xm1014;mag7;p90;cz75a;mp9;mp7;mp5sd;bizon;mac10;ump45;m4a1;m4a1_silencer;galilar;ak47;aug;famas;g3sg1;scar20;awp;sg556;ssg08;m249;negev"
	}
	es wcsgroup set evasion event_var(userid) 0
	es wcsgroup set evasion_chance event_var(userid) 0
	es wcs_setcooldown event_var(userid) 8
	es_delayed .5 wcs_color event_var(userid) 75 15 255 255 0
	es wcs_setfx speed event_var(userid) = 1.3 0
	es_give event_var(userid) weapon_knife
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Spawn (END)
	}
}

block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Death (START)
	}
	es wcsgroup set evasion event_var(userid) 0
	es wcsgroup set evasion_chance event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] OnChange (START)
	}
	es wcsgroup set evasion server_var(wcs_userid) 0
	es wcsgroup set evasion_chance server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] OnChange (END)
	}
}

block blade
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 1 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		if (event_var(es_attackerdead) = 0) do
		{
			if (event_var(weapon) == knife) do
			{
				es damage event_var(userid) server_var(wcs_dmg) 0 event_var(attacker)
				es wcs_centertell event_var(userid) Took + server_var(wcs_dmg) damage from a nether blade
				es wcs_centertell event_var(attacker) Nether blade did + server_var(wcs_dmg) damage
				es wcs_fade server_var(wcs_userid) 55 125 55 200 1 1
				es_emitsound player event_var(userid) weapons\airboat\airboat_gun_energy2.wav 1 0.5
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_emitsound player event_var(userid) weapons\airboat\airboat_gun_energy2.wav 1 0.5
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(attacker) weapons\knife\knife_slash2.wav .7
					es_playsound event_var(userid) weapons\knife\knife_slash2.wav .7
					es_delayed .3 es_playsound event_var(attacker) weapons\knife\knife_slash1.wav .7
					es_delayed .3 es_playsound event_var(userid) weapons\knife\knife_slash1.wav .7
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 1 (END)
	}
}

block armor
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 2 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		es wcsgroup set evasion event_var(userid) 1
		es wcsgroup set evasion_chance event_var(userid) server_var(wcs_chance)
	    es wcsgroup get evasion_count wcs_tmp1 event_var(userid)
	    es_tell event_var(userid) #multi #green [WCS]#lightgreenEvasion - You evaded server_var(wcs_tmp1) attacks last round
		es wcs_fade event_var(userid) 55 55 155 200 1 1
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 2 (END)
	}
}

block pull
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
				es_xmath wcs_z1 - 5
				es_setplayerprop event_var(attacker) CBasePlayer.localdata.m_vecBaseVelocity 0,0,250
				es_createvectorstring wcs_tmp server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es wcs_pushto event_var(attacker) server_var(wcs_tmp) 2
				es_tell event_var(attacker) #multi #green [WCS]#lightgreen Singularity - Pulled you towards event_var(es_username)
				es_tell event_var(userid) #multi #green [WCS]#lightgreen Singularity - Pulled event_var(es_attackername) towards you
				es wcs_fade event_var(attacker) 155 25 255 200 1 1
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_emitsound player event_var(userid) ambient\levels\citadel\portal_beam_shoot6.wav 1 0.5
					es_emitsound player event_var(attacker) ambient\levels\citadel\portal_beam_shoot6.wav 1 0.5	
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 3 (END)
	}
}

block teleport
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 4 (START)
	}
	es wcs_doteleport server_var(wcs_userid)
	es wcs_fade server_var(wcs_userid) 155 25 255 200 1 2
	es wcs_color server_var(wcs_userid) 155 55 255 5 1
	es_delayed 1 wcs_fade server_var(wcs_userid) 155 25 255 200 1 2
	es_delayed 1 wcs_color server_var(wcs_userid) 155 55 255 255 1
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_emitsound player server_var(wcs_userid) weapons\physcannon\energy_disintegrate5.wav 1 1
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
	}
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 25 40 1 50 100 2 60 155 55 155 2
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 25 40 1 50 100 2 60 155 55 255 2
	es_xmath wcs_z2 + 32
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 25 40 1 50 100 2 125 55 255 155 2
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 25 40 1 50 100 2 125 55 255 255 2
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rift Walker - [Block] Skill: 4 (END)
	}
}
