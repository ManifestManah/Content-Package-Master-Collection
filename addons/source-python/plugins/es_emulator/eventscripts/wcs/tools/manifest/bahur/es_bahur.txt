//////////////////////
//Manifest Race Pack//
//Version 1.0.1B    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Spawn (START)
	}
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_delayed .1 es_give event_var(userid) weapon_tmp
		es_playsound event_var(userid) ambient/materials/metal4.wav .6
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es_delayed .1 es_give event_var(userid) mp7
		es_playsound event_var(userid) ambient\misc\metal_str4.wav .6
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 10
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 330 331 3 60 10 0 100 100 255 200 5
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 75 300 3 60 10 0 100 100 255 200 5
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Spawn (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 1 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_xmath wcs_speed * 100
	es_xmath wcs_speed - 100
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Arctic environment - Allows you to run server_var(wcs_speed)% faster
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 1 (END)
	}
}

block leech
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do 
			{
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
				es_xmath wcs_z1 + 20
				es_xmath wcs_z2 + 20
				es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
				es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector2) server_var(vector1) 5 1 15 15 0 0 100 100 230 255 15
				es est_effect_06 #a 0 effects/blueflare1.vmt server_var(vector2) server_var(vector1) 5 1 30 30 0 0 200 200 200 255 20
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 155 65 1 70 10 0 60 60 255 255 15
				es est_effect 10 #a 0 sprites/blueflare1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 155 65 1 20 10 0 100 100 200 255 20
				es playerget health wcs_tmp2 event_var(attacker)
				if (server_var(wcs_tmp2) < 260) do
				{
					es_xrand wcs_addhealth 12 20
					es wcs_setfx health event_var(attacker) + server_var(wcs_addhealth) 0
					es wcs_centertell event_var(attacker) Leeched server_var(wcs_addhealth) health!
				}
				if (server_var(wcs_tmp2) => 260) do
				{
					es wcs_setfx health event_var(attacker) = 260 0
					es wcs_centertell event_var(attacker) You have reached the 260 health cap!
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 2 (END)
	}
}

block invis
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 3 (START)
	}
	es_delayed .5 wcs_color event_var(userid) 255 255 255 server_var(wcs_invis) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Pure ice - Made you less visible
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 3 (END)
	}
}

block damage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice4) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_xrand wcs_dmg 4 13
				es playsound event_var(userid) skills\frostbolt.mp3 1
				es playsound event_var(attacker) skills\frostbolt.mp3 .4
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_centertell event_var(attacker) Penetrating ice shards did + server_var(wcs_dmg) damage!
				es wcs_centertell event_var(userid) Took + server_var(wcs_dmg) from some penetrating ice shards!		
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z1 + 25
				es_xmath wcs_z2 + 25
				es_xmath wcs_x2 + 25
				es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
				es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector1) server_var(vector2) 1 .5 8 8 0 0 100 100 255 255 20
				es_xmath wcs_x2 - 50
				es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
				es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector1) server_var(vector2) 1 .5 8 8 0 0 100 100 255 255 20
				es est_effect_08 #a 0 sprites/lgtning.vmt server_var(vector2) 100 375 1 0.5 75 10 .4 100 100 255 255 2 1
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 4 (END)
	}
}

block breeze
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 5 (START)
	}
	es_xset wcs_execute_ultimate 0
	es_getplayerteam wcs_team server_var(wcs_userid)
	if (server_var(wcs_team) = 2) do
	{
		es_xset wcs_targetteam "#ct,#alive"
		es_getlivingplayercount wcs_pcount 3
	}
	if (server_var(wcs_team) = 3) do
	{
		es_xset wcs_targetteam "#t,#alive"
		es_getlivingplayercount wcs_pcount 2
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_range) "es_xdoblock wcs/tools/manifest/bahur/coldbreeze_path"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				//everything related to the wcs_userid
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es playsound server_var(wcs_userid) ambient\levels\canals\windmill_wind_loop1.wav 1
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es playsound server_var(wcs_userid) ambient\wind\css15_wind_02.wav 1
				}
				es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Cold Breeze - Slowed down server_var(wcs_count_players_actually_affected) enemies
				es wcs_centertell server_var(wcs_userid) You've summoned a Cold breeze affecting server_var(wcs_count_players_actually_affected) enemies
				es playerget viewcoord server_var(wcs_userid) wcs_2 wcs_y2 wcs_z2
				es_xmath wcs_z2 + 10
				es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_2) server_var(wcs_y2) server_var(wcs_z2) 300 301 2 60 10 0 100 100 255 200 5
				es_xmath wcs_z2 + 25
				es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_2) server_var(wcs_y2) server_var(wcs_z2) 75 280 2 60 10 0 100 100 255 200 5
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you need to aim at an enemy
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 5 (END)
	}
}

block coldbreeze_path 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 5 (CHECKS START)
	}
	es_exists ex userid server_var(wcs_targetid)
	if (server_var(ex) = 1) do
	{
		es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
		if (server_var(wcs_alive) = 0) do
		{
			es_xmath wcs_counter + 1
			if (server_var(wcs_counter) > 0) do
			{
				if (server_var(wcs_counter) <= 20) do
				{
					es_set wcs_initial_players_struck server_var(wcs_counter)
					es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
					if (server_var(wcs_tmp) != 1) do
					{
						es_xset wcs_ultinotexec 0
						es_xset wcs_execute_ultimate 1
						es_xmath wcs_count_players_actually_affected + 1
						es_xdoblock wcs/tools/manifest/bahur/coldbreeze_final
					}
					if (server_var(wcs_tmp) == 1) do
					{
						es_xmath wcs_count_players_ultimate_immunity + 1
						if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's ultimate
							es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
						}
						if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, all nearby enemy targets were immune!
							es_xset wcs_ultinotexec 1
						}
					}
				}
				else do
				{
					es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you can't hit that many targets at once!
					es_xset wcs_ultinotexec 1
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 5 (CHECKS END)
	}
}

block coldbreeze_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 5 (EXECUTED START)
	}
	es wcsgroup get is_slowed wcs_tmp3 server_var(wcs_targetid)
	if (server_var(wcs_tmp3) == 0) do
	{
		es wcsgroup set is_slowed server_var(wcs_targetid) 1
		es_delayed 2 wcsgroup set is_slowed server_var(wcs_targetid) 0
		es_getgame wcs_gamecheck
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es playsound server_var(wcs_targetid) ambient\levels\canals\windmill_wind_loop1.wav 1
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es playsound server_var(wcs_targetid) ambient\wind\css15_wind_02.wav 1
		}
		es wcs_centertell server_var(wcs_targetid) You were caught in a cold breeze
		es wcs_setfx speed server_var(wcs_targetid) = 0.8 2
	}
	if (server_var(wcs_tmp3) == 1) do
	{
		es wcs_centertell server_var(wcs_userid) Your target was immune to slows!
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Bahur - The Frost Demon - [Block] Skill: 5 (EXECUTED END)
	}
}
