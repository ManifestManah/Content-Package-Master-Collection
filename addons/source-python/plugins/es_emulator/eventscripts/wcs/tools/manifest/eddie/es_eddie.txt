/////////////////////////
//Holliday V2 Race Pack//
//Version 1.0.0D       //
/////////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Spawn (START)
	}
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
	es_xmath wcs_z2 + 60
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 100 200 2.5 10 10 25 163 73 164 255 255
	es_xmath wcs_z2 - 10
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 100 200 2.5 10 10 25 163 73 164 255 255
	es_xmath wcs_z2 - 10
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 100 200 2.5 10 10 25 163 73 164 255 255
	es_xmath wcs_z2 - 10
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 100 200 2.5 10 10 25 163 73 164 255 255
	es_xmath wcs_z2 - 10
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 100 200 2.5 10 10 25 163 73 164 255 255
	es_xmath wcs_z2 - 10
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 100 200 2.5 10 10 25 163 73 164 255 255
	es_delayed .1 es_give event_var(userid) weapon_hegrenade
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Spawn (END)
	}
}

block powerslave
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= 5) do 
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcsgroup get is_frozen wcs_tmp4 event_var(userid)
				if (server_var(wcs_tmp4) == 0) do
				{
					es wcsgroup set is_frozen event_var(userid) 1
					es_delayed server_var(wcs_duration) wcsgroup set is_frozen event_var(userid) 0
					es wcs_shake event_var(userid) 12 40 1
					es wcs_setfx freeze event_var(userid) = 1 server_var(wcs_duration)
					es wcs_centertell event_var(userid) event_var(es_attackername) electrified you!
					es wcs_centertell event_var(attacker) You electrified event_var(es_username)!
					es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
					es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
					es_xmath wcs_z1 + 20
					es_xmath wcs_z2 + 20
					es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 10 10 163 73 164 255
					es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 10 10 163 73 164 255
					es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 2 2 163 73 164 255
					es est_effect 10 #a 0 sprites/smoke.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 10 0.5 90 128 5 163 73 164 255 255
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 1 (END)
	}
}

block trooper
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 2 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
		es_delayed .2 wcs_setfx gravity event_var(userid) = server_var(wcs_gravity) 0
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Trooper - Increased your speed and reduced your gravity
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_xmath wcs_z1 + 50
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 500 3 10 50 1 100 0 25 255 3
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 2 (END)
	}
}

block bomb
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 3 (START)
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 35
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector1) 1 10 5 200 10 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) metal
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team event_var(userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}	
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
			es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius) "es_xdoblock wcs/tools/manifest/eddie/wcs_ulti_suicide_checks"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				es wcs_centertell event_var(userid) The Explosion damaged server_var(wcs_count_players_actually_affected) enemies for 100 damage
				es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion affected server_var(wcs_count_players_actually_affected) enemies
				es_xrand wcs_rand 1 3
				if (server_var(wcs_rand) == 1) do
				{
					es_playsound event_var(userid) weapons\hegrenade\explode5.wav .7
				}
				if (server_var(wcs_rand) == 2) do
				{
					es_playsound event_var(userid) weapons\hegrenade\explode4.wav .8
				}
				if (server_var(wcs_rand) == 3) do
				{
				es_playsound event_var(userid) weapons\hegrenade\explode3.wav .7
				}
			}
		}
		else do
		{
			es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, no nearby enemies!
		}
	}
	else do
	{
		es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Could not target an enemy team
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 3 (END)
	}
}

block wcs_ulti_suicide_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 3 (CHECKS START)
	}
	es_exists ex userid server_var(wcs_targetid)
	if (server_var(ex) = 1) do
	{
		es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
		if (server_var(wcs_alive) = 0) do
		{
			es_xmath wcs_counter + 1
			if (server_var(wcs_counter) > 0) do
			{
				if (server_var(wcs_counter) <= 20) do
				{
					es_set wcs_initial_players_struck server_var(wcs_counter)
					es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
					if (server_var(wcs_tmp) != 1) do
					{
						es_xset wcs_execute_ultimate 1
						es_xmath wcs_count_players_actually_affected + 1
						es_xdoblock wcs/tools/manifest/eddie/wcs_ulti_suicide_final
					}
					if (server_var(wcs_tmp) == 1) do
					{
						es_xmath wcs_count_players_ultimate_immunity + 1
						if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's explosion
							es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
						}
						if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
						{
							es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, all nearby enemy targets were immune!
						}
					}
				}
				else do
				{
					es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, you can't hit that many targets at once!
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 3 (CHECKS END)
	}
}

block wcs_ulti_suicide_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 3 (EXECUTED START)
	}
	es wcs_centertell server_var(wcs_targetid) You took 100 damage from an Explosion
	es_xrand wcs_rand 1 3
	if (server_var(wcs_rand) == 1) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode5.wav .7
	}
	if (server_var(wcs_rand) == 2) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode4.wav .8
	}
	if (server_var(wcs_rand) == 3) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode3.wav .7
	}
	es damage server_var(wcs_targetid) 100 1 event_var(userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 3 (EXECUTED END)
	}
}

block holynade
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice3) <= 75) do
	{
		if (event_var(weapon) == hegrenade) do
		{
			if (event_var(es_userdead) = 0) do
			{
				if (event_var(es_attackerdead) = 0) do
				{
					es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
					es_tell event_var(attacker) #multi #green [WCS]#lightgreen Holy nade - Dealt server_var(wcs_dmg) additional damage
					es_tell event_var(userid) #multi #green [WCS]#lightgreen Holy nade - Recieved server_var(wcs_dmg) additional damage
					es_getgame wcs_gamecheck
					if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
					{
						es_playsound event_var(userid) weapons/motar/motar_explode3.wav 1
						es_playsound event_var(attacker) weapons/motar/motar_explode3.wav 1
					}
					if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
					{
						es_playsound event_var(userid) weapons\hegrenade\explode4.wav 1
						es_playsound event_var(attacker) weapons\hegrenade\explode4.wav 1
					}
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Eddie Maiden - [Block] Skill: 4 (END)
	}
}
