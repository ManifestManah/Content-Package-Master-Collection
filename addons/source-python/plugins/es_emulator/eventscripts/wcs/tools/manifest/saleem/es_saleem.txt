//////////////////////
//Manifest Race Pack//
//Version 1.0.1B    //
//////////////////////

block lifeforce
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				if (event_var(es_userteam) = 2) do
				{
					es_xset wcs_team #t,#alive
				}
				if (event_var(es_userteam) = 3) do
				{
					es_xset wcs_team #ct,#alive
				}
				es wcs_foreach player wcs_player server_var(wcs_team) "es_xdoblock wcs/tools/manifest/saleem/lifeforce_exe"
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 1 (END)
	}
}

block lifeforce_exe
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 4 (LIFEFORCE_EXE EXECUTED START)
	}
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_stopsound server_var(wcs_player) vehicles/v8/skid_highfriction.wav
		es_playsound server_var(wcs_player) vehicles/v8/skid_highfriction.wav .6
	}
	es_xrand wcs_addhealth 5 15
	es wcs_setfx health server_var(wcs_player) + server_var(wcs_addhealth) 0
	es wcs_setfx health event_var(userid) - server_var(wcs_addhealth) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Life Force - You've channeled server_var(wcs_addhealth) health into your allies
	es_tell server_var(wcs_player) #multi #green [WCS]#lightgreen Life Force - You've channeled server_var(wcs_addhealth) health into your allies
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect_14 #a 0 effects/redflare_v2.vmt server_var(vector2) server_var(vector1) 225 22 125
	es est_effect_14 #a 0 effects/redflare_v2.vmt server_var(vector2) server_var(vector1) 215 28 125
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 4 (LIFEFORCE_EXE EXECUTED END)
	}
}

block revitalize
{
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 1) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				if (event_var(es_userteam) = 2) do
				{
					es_xset wcs_team #t,#alive
				}
				if (event_var(es_userteam) = 3) do
				{
					es_xset wcs_team #ct,#alive
				}
				es wcs_foreach player wcs_player server_var(wcs_team) "es_xdoblock wcs/tools/manifest/saleem/revitalize_exe"
			}
		}
	}
}

block revitalize_exe
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 2 (START)
	}
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_stopsound event_var(userid) ambient/levels/citadel/portal_beam_shoot5.wav
		es_playsound event_var(userid) ambient/levels/citadel/portal_beam_shoot5.wav 1.0
		es_stopsound server_var(wcs_player) ambient/levels/citadel/portal_beam_shoot5.wav
		es_playsound server_var(wcs_player) ambient/levels/citadel/portal_beam_shoot5.wav 1.0
	}
	es_xrand wcs_tmp1 75 150
	es wcs_setfx health server_var(wcs_player) + server_var(wcs_tmp1) 3
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Revitalize - You've granted your team server_var(wcs_tmp1) health for 3 seconds
	es_tell server_var(wcs_player) #multi #green [WCS]#lightgreen Revitalize - You've received server_var(wcs_tmp1) health for 3 seconds!
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_player)
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_player)
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect_14 #a 0 effects/blueflare1.vmt server_var(vector2) server_var(vector1) 225 24 130
	es est_effect_14 #a 0 effects/yellowflare_noz.vmt server_var(vector2) server_var(vector1) 225 16 120
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 2 (END)
	}
}

block limbo
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 3 (START)
	}
	es wcsgroup get has_used_phoenix wcs_tmp3 event_var(userid)
	if (server_var(wcs_tmp3) == 0) do
	{
		es wcsgroup set has_used_phoenix event_var(userid) 1
		es wcsgroup get phoenix wcs_phoenix event_var(attacker) event_var(es_userteam)
		es_xmath wcs_phoenix + 1
		es wcsgroup set phoenix event_var(attacker) server_var(wcs_phoenix) event_var(es_userteam)
		es wcs_centertell event_var(attacker) You've dragged a soul out of Limbo!
		es_tell event_var(attacker) #multi #green [WCS]#lightgreen Limbo - The first ally who dies will come back to life
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 3 (END)
	}
}

block drainsoul
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_playsound event_var(attacker) wcs/vampiricstrike.mp3 .8
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
				es_xmath wcs_z1 + 20
				es_xmath wcs_z2 + 20
				es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
				es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector2) server_var(vector1) 5 1 15 15 0 .2 128 0 0 175 15
				es est_effect_06 #a 0 effects/blueflare1.vmt server_var(vector2) server_var(vector1) 5 1 30 30 0 0 180 0 0 255 20
				es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 155 65 1 30 10 0 128 0 0 175 15
				es est_effect 10 #a 0 sprites/blueflare1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 155 65 1 20 10 0 180 0 0 255 20
				es_xrand wcs_tmp2 0.20 0.35
				es wcs_setfx longjump event_var(attacker) + server_var(wcs_tmp2) 0
				es wcs_setfx speed event_var(attacker) + .04 0
				es_delayed .2 wcs_setfx gravity event_var(userid) - server_var(wcs_tmp2) 0
				es_tell event_var(attacker) #multi #green [WCS]#lightgreen Drain soul - Improved your vitality, speed, longjump & gravity
				es playerget health wcs_tmp2 event_var(attacker)
				if (server_var(wcs_tmp2) < 250) do
				{
					es_xrand wcs_addhealth 5 11
					es wcs_setfx health event_var(attacker) + server_var(wcs_addhealth) 0
					es wcs_centertell event_var(attacker) You drained server_var(wcs_addhealth) health!
				}
				if (server_var(wcs_tmp2) => 250) do
				{
					es wcs_setfx health event_var(attacker) = 250 0
					es wcs_centertell event_var(attacker) You have reached the 250 health cap!
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 4 (END)
	}
}

block deathpact
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 5 (START)
	}
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es_xrand wcs_dmg 1 5
	es damage server_var(wcs_userid) server_var(wcs_dmg) 1 server_var(wcs_userid)
	es_tell event_var(attacker) #multi #green [WCS]#lightgreen Death Pact - Consumed server_var(wcs_dmg) of your health!
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es_xmath wcs_z1 + 15
	es_xmath wcs_z2 + 15
	es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
	es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 155 65 1 30 10 0 128 0 0 175 15
	es est_effect 10 #a 0 sprites/blueflare1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 155 65 1 20 10 0 180 0 0 255 20
	es est_effect_14 #a 0 effects/redflare_v2.vmt server_var(vector2) server_var(vector1) 225 22 125
	es est_effect_14 #a 0 effects/redflare_v2.vmt server_var(vector2) server_var(vector1) 215 28 125
	es_xset wcs_range 1250
	es_xset wcs_ok 1
	es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es_xcopy wcs_x wcs_x1
	es_xcopy wcs_y wcs_y1
	es_xcopy wcs_z wcs_z1
	es_math wcs_x - server_var(wcs_x2)
	es_math wcs_y - server_var(wcs_y2)
	es_math wcs_z - server_var(wcs_z2)
	if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
	if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
	if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1
	ifx parse("(wcs_range < wcs_x) or (wcs_range < wcs_y) or (wcs_range < wcs_z)") do
	{
		es_xset wcs_ok 0
	}
	ifx true(wcs_ok) do
	{
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es wcs_pushto server_var(wcs_userid) server_var(vector1) 1.8
	}
	es_xelse do
	{
		if (server_var(wcs_range) < server_var(wcs_x)) do
		{
			if (server_var(wcs_x2) >= 0) do
			{
				if (server_var(wcs_x1) >= 0) do
				{
					if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
					if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_x2 - server_var(wcs_range)
				}
			}
			es_xelse do
			{
				if (server_var(wcs_x1) < 0) do
				{
					if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
					if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
				}	
				es_xelse do
				{
					es_math wcs_x2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_x1 wcs_x2
		}
		if (server_var(wcs_range) < server_var(wcs_y)) do
		{
			if (server_var(wcs_y2) >= 0) do
			{
				if (server_var(wcs_y1) >= 0) do
				{
					if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
					if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_y2 - server_var(wcs_range)
				}
			}
			es_xelse do
			{
				if (server_var(wcs_y1) < 0) do
				{
					if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
					if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_y2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_y1 wcs_y2
		}
		if (server_var(wcs_range) < server_var(wcs_z)) do
		{
			if (server_var(wcs_z2) >= 0) do
			{
				if (server_var(wcs_z1) >= 0) do
				{
					if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
					if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_z2 - server_var(wcs_range)
				}
			}
			es_xelse do
			{
				if (server_var(wcs_z1) < 0) do
				{
					if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
					if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_z2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_z1 wcs_z2
		}
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es wcs_pushto server_var(wcs_userid) server_var(vector1) 1.8
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Saleem - Chanter Of Death - [Block] Skill: 5 (END)
	}
}