//////////////////////
//Kamiqawa Race Pack//
//Version 1.1.0A    //
//////////////////////


block death
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Death (START)
	}
	es wcsgroup set rocketeerjump event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Death (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] OnChange (START)
	}
	es wcsgroup set rocketeerjump server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] OnChange (END)
	}
}

block armour
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		es_delayed .2 wcs_setfx health event_var(userid) + 10 0
		ifx false(wcs_falldamage_compensation) do
		{
			es wcs_setfx falldamage event_var(userid) = 1.0 0
			es_tell event_var(userid) #multi #green [WCS]#lightgreen Seismic armour - Gave you 10 health and made you immune to fall damage!
		}
		ifx true(wcs_falldamage_compensation) do
		{
			es_delayed .5 wcs_setfx health event_var(userid) + 25 0
			es_tell event_var(userid) #multi #green [WCS]#lightgreen Falldamage Compensation - You've been compensated with 25 extra health
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 1 (END)
	}
}

block jump
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		es wcsgroup set rocketeerjump event_var(userid) 1
		es_delayed .2 wcs_setfx gravity event_var(userid) = .8 0
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Rocket jump - Allows you to jump higher
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 2 (END)
	}
}

event player_jump
{	
	es wcsgroup get rocketeerjump wcs_tmp3 event_var(userid)
	if (server_var(wcs_tmp3) == 1) do
	{
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Rocketeer - [Block] Event Player_Jump (START)
		}
		es_getgame wcs_gamecheck
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_playsound event_var(userid) weapons/grenade_launcher1.wav .3
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_userid) weapons\hegrenade\explode4.wav 1
		}
		es wcs_pushed event_var(userid) 0 0 475
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 100 .5 10 10 0 80 80 80 255 3
		es est_effect 7 #a 0 sprites/smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 100 1
		ifx true(wcs_race_debugging) do
		{
			wcs_log [Race]: Rocketeer - [Block] Event Player_Jump (END)
		}
	}
}

block damage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_xset wcs_tmp4 weapons/shotgun/shotgun_dbl_fire.wav
					es_playsound event_var(userid) weapons/shotgun/shotgun_dbl_fire.wav .6
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es_xset wcs_tmp4 weapons\hegrenade\explode4.wav
					es_playsound event_var(userid) weapons\hegrenade\explode4.wav 1
				}
				es wcs_centertell event_var(attacker) Rocket turret dealt + server_var(wcs_dmg) damage
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				ifx true(wcs_ffa_enabled) do
				{
					es_xset wcs_targetteam #alive
				}
				ifx false(wcs_ffa_enabled) do
				{
					es_getplayerteam wcs_team event_var(attacker)
					if (server_var(wcs_team) = 2) then es_xset wcs_targetteam #ct
					if (server_var(wcs_team) = 3) then es_xset wcs_targetteam #t
				}
				es wcs_nearcoord wcs_player server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 75 "es damage server_var(wcs_player) server_var(wcs_dmg) 1 event_var(attacker);es_playsound server_var(wcs_player) server_var(wcs_tmp4) 1"
				es_xmath wcs_z1 + 25
				es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector1) 1 10 5 200 10 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) metal
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 3 (END)
	}
}

block boost
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 4 (START)
	}
	es_xset wcs_ok 1
	es_getgame wcs_gamecheck
	if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
	{
		es_playsound server_var(wcs_userid) weapons/explode5.wav .8
	}
	if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
	{
		es_playsound server_var(wcs_userid) weapons\hegrenade\explode5.wav 1
	}
	es wcs_setfx freeze server_var(wcs_userid) = 1 server_var(wcs_duration)
	es wcsgroup set is_frozen server_var(wcs_userid) 1
	es_delayed server_var(wcs_duration) wcsgroup set is_frozen server_var(wcs_userid) 0
	es_xset wcs_range 2000

	es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es est_effect 7 #a 0 sprites/smoke.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 40 1
	es_delayed .3 est_effect 7 #a 0 sprites/smoke.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 40 1
	es est_effect 11 #a 0 sprites/ar2_muzzle4b.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 1 200

    es_xcopy wcs_x wcs_x1
    es_xcopy wcs_y wcs_y1
    es_xcopy wcs_z wcs_z1

    es_math wcs_x - server_var(wcs_x2)
    es_math wcs_y - server_var(wcs_y2)
    es_math wcs_z - server_var(wcs_z2)

    if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
    if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
    if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1

    if (server_var(wcs_range) < server_var(wcs_x)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_y)) do
    {
		es_xset wcs_ok 0
    }
    if (server_var(wcs_range) < server_var(wcs_z)) do
    {
		es_xset wcs_ok 0
    }
    
    ifx true(wcs_ok) do
    {
 	    ifx true(wcs_legacy_teleport_fade) do
	    {
	    	es wcs_fade server_var(wcs_userid) 65 255 30 50 1 1
	    }
 	    ifx true(wcs_legacy_teleport_type) do
	    {
	    	//To Do: Improve this by finding an alternative teleportation method that prevent players from getting stuck in displacemnts and models
	    	es playerset location server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    	es_delayed .01 wcs_teleport server_var(wcs_userid) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	    }
	    ifx false(wcs_legacy_teleport_type) do
	    {
	    	es_setplayerprop server_var(wcs_userid) CBasePlayer.localdata.m_vecBaseVelocity 0,0,250
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) 2
	    }
        es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenRocket charge - Riding on a launched rocket
        es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
    }
    
    else do
    {
        if (server_var(wcs_range) < server_var(wcs_x)) do
        {
            if (server_var(wcs_x2) >= 0) do
            {
                if (server_var(wcs_x1) >= 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_x1) < 0) do
                {
                    if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
                    if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_x2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_x1 wcs_x2
        }
        
        if (server_var(wcs_range) < server_var(wcs_y)) do
        {
            if (server_var(wcs_y2) >= 0) do
            {
                if (server_var(wcs_y1) >= 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_y1) < 0) do
                {
                    if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
                    if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_y2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_y1 wcs_y2
        }
        
        if (server_var(wcs_range) < server_var(wcs_z)) do
        {
            if (server_var(wcs_z2) >= 0) do
            {
                if (server_var(wcs_z1) >= 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 - server_var(wcs_range)
                }
            }
            else do
            {
                if (server_var(wcs_z1) < 0) do
                {
                    if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
                    if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
                }
                else do
                {
                    es_math wcs_z2 + server_var(wcs_range)
                }
            }
            es_xcopy wcs_z1 wcs_z2
        }

	    ifx false(wcs_legacy_teleport_type) do
	    {
	        es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
	        es_delayed .01 wcs_pushto server_var(wcs_userid) server_var(vector1) .1
	    }
 	    ifx true(wcs_legacy_teleport_fade) do
	    {
	    	es wcs_fade server_var(wcs_userid) 65 255 30 50 1 1
	    }
        es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreenRocket charge - You can't rocket charge that far!
	    es wcs_setcooldown server_var(wcs_userid) 0
    }
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Rocketeer - [Block] Skill: 4 (END)
	}
}
