/////////////////////
//Damakex Race Pack//
//Version 1.0.1D   //
/////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Spawn (START)
	}
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 vgui/gfx/vgui/cs_logo.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 650 20 2 20 10 0 255 0 0 255 1
	es_delayed .2 es est_effect 10 #a 0 sprites/scanner.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 650 20 2 35 10 0 255 0 0 255 1
	es_xmath wcs_z1 + 20
	es est_effect 10 #a 0 vgui/gfx/vgui/cs_logo.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 650 20 2 20 10 0 255 0 0 255 1
	es_delayed .2 es est_effect 10 #a 0 sprites/scanner.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 650 20 2 35 10 0 255 0 0 255 1
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Spawn (END)
	}
}

block bullets
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= 25) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_centertell event_var(attacker) You dealt + server_var(wcs_dmg) damage with a laser bullet
				es_getgame wcs_gamecheck
				if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor03.wav 1
					es_playsound event_var(attacker) weapons/fx/nearmiss/bulletltor03.wav 1
					es_delayed .2 es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor03.wav 1
					es_delayed .2 es_playsound event_var(attacker) weapons/fx/nearmiss/bulletltor03.wav 1
				}
				if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
				{
					es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor09.wav 1
					es_playsound event_var(attacker) weapons/fx/nearmiss/bulletltor09.wav 1
					es_delayed .1 es_playsound event_var(userid) weapons/fx/nearmiss/bulletltor13.wav 1
					es_delayed .1 es_playsound event_var(attacker) weapons/fx/nearmiss/bulletltor13.wav 1
				}
				es_xrand wcs_red 70 255
				es_xrand wcs_green 70 255
				es_xrand wcs_blue 70 255
				es_xrand wcs_alpha 125 255
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
				es_xmath wcs_z1 + 25
				es_xmath wcs_z2 + 25
				es est_effect 3 #a 0 sprites/scanner_bottom.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 8 8 255 255 255 255
				es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 10 10 server_var(wcs_red) server_var(wcs_green) server_var(wcs_blue) server_var(wcs_alpha)
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 1 (END)
	}
}

block recover
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 2 (START)
	}
	es_xrand wcs_duration 5 10
	es wcs_regeneration event_var(userid) server_var(wcs_addhealth) server_var(wcs_duration) 175 1000 250
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Metallic Skin - You'll recover server_var(wcs_addhealth) health every server_var(wcs_duration) seconds when you are below 175 health
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 2 (END)
	}
}

block disguise
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			es_delayed .5 wcs_setfx disguiser event_var(userid) = 1 0
			es_tell event_var(userid) #multi #green [WCS]#lightgreen Organic Skin - You look like an enemy for the rest of this round!
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
			es_xmath wcs_z1 + 20
			es est_effect 10 #a 0 vgui/gfx/vgui/cs_logo.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 450 2 20 10 0 255 0 0 255 1
			es_delayed .2 es est_effect 10 #a 0 sprites/scanner.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 450 2 35 10 0 255 0 0 255 1
			es_xmath wcs_z1 + 20
			es est_effect 10 #a 0 vgui/gfx/vgui/cs_logo.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 450 2 20 10 0 255 0 0 255 1
			es_delayed .2 es est_effect 10 #a 0 sprites/scanner.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 20 450 2 35 10 0 255 0 0 255 1
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 3 (END)
	}
}

block bomb
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 4 (START)
	}
	if (server_var(wcs_dice4) <= 75) do
	{
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_xmath wcs_z1 + 35
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es est_effect_20 #a 0 sprites/lgtning.vmt server_var(vector1) 1 10 5 200 10 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) metal
		ifx true(wcs_ffa_enabled) do
		{
			es_xset wcs_targetteam "#alive"
			es_getlivingplayercount wcs_pcount
		}
		ifx false(wcs_ffa_enabled) do
		{
			es_getplayerteam wcs_team event_var(userid)
			if (server_var(wcs_team) = 2) do
			{
				es_xset wcs_targetteam "#ct,#alive"
				es_getlivingplayercount wcs_pcount 3
			}	
			if (server_var(wcs_team) = 3) do
			{
				es_xset wcs_targetteam "#t,#alive"
				es_getlivingplayercount wcs_pcount 2
			}
		}
		if (server_var(wcs_targetteam) != 0) do
		{
			if (server_var(wcs_pcount) > 0) do
			{
				es_xset wcs_counter 0
				es_xset wcs_initial_players_struck 0
				es_xset wcs_count_players_ultimate_immunity 0
				es_xset wcs_count_players_actually_affected 0
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es wcs_nearcoord wcs_targetid server_var(wcs_targetteam) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 300 "es_xdoblock wcs/tools/manifest/terminator/wcs_ulti_suicide_checks"
				if (server_var(wcs_execute_ultimate) == 1) do
				{
					//No matter how many targets are affected, this section will only be executed once.
					//If you have a global counter you wish to retrieve then use this section, also good for general mesasges or commands you only want to happen on the wcs_userid / event_var(userid)
					
					es wcs_centertell event_var(userid) The Explosion damaged server_var(wcs_count_players_actually_affected) enemies for server_var(wcs_magnitude) damage
					es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion affected server_var(wcs_count_players_actually_affected) enemies
					es_xrand wcs_rand 1 3
					if (server_var(wcs_rand) == 1) do
					{
						es_playsound event_var(userid) weapons\hegrenade\explode5.wav .7
					}
					if (server_var(wcs_rand) == 2) do
					{
						es_playsound event_var(userid) weapons\hegrenade\explode4.wav .8
					}
					if (server_var(wcs_rand) == 3) do
					{
					es_playsound event_var(userid) weapons\hegrenade\explode3.wav .7
					}
				}
			}
			else do
			{
				es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, there are no living enemies!
			}
			if (server_var(wcs_counter) = 0) do
			{
				es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, no nearby enemies!
			}
		}
		else do
		{
			es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Could not target an enemy team
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 4 (END)
	}
}

block wcs_ulti_suicide_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 4 (CHECKS START)
	}
	es_exists ex userid server_var(wcs_targetid)
	if (server_var(ex) = 1) do
	{
		es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
		if (server_var(wcs_alive) = 0) do
		{
			es_xmath wcs_counter + 1
			if (server_var(wcs_counter) > 0) do
			{
				if (server_var(wcs_counter) <= 20) do
				{
					es_set wcs_initial_players_struck server_var(wcs_counter)
					es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
					if (server_var(wcs_tmp) != 1) do
					{
						es_xset wcs_execute_ultimate 1
						es_xmath wcs_count_players_actually_affected + 1
						es_xdoblock wcs/tools/manifest/terminator/wcs_ulti_suicide_final
					}
					if (server_var(wcs_tmp) == 1) do
					{
						es_xmath wcs_count_players_ultimate_immunity + 1
						if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
						{
							es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's explosion
							es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
						}
						if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
						{
							es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, all nearby enemy targets were immune!
						}
					}
				}
				else do
				{
					es_tell event_var(userid) #multi #green [WCS]#lightgreen The Explosion - Failed, you can't hit that many targets at once!
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 4 (CHECKS END)
	}
}

block wcs_ulti_suicide_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 4 (EXECUTED START)
	}
	es wcs_centertell server_var(wcs_targetid) You took server_var(wcs_magnitude) damage from an Explosion
	es_xrand wcs_rand 1 3
	if (server_var(wcs_rand) == 1) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode5.wav .7
	}
	if (server_var(wcs_rand) == 2) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode4.wav .8
	}
	if (server_var(wcs_rand) == 3) do
	{
		es_playsound server_var(wcs_targetid) weapons\hegrenade\explode3.wav .7
	}
	es damage server_var(wcs_targetid) server_var(wcs_magnitude) 1 event_var(userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Terminator - [Block] Skill: 4 (EXECUTED END)
	}
}