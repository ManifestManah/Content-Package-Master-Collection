////////////////////
//Teller Race Pack//
//Version 1.0.1D  //
////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Spawn (START)
	}
	es wcsgroup set evasion event_var(userid) 0
	es wcsgroup set evasion_chance event_var(userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Spawn (END)
	}
}

block onchange
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] OnChange (START)
	}
	es wcsgroup set evasion server_var(wcs_userid) 0
	es wcsgroup set evasion_chance server_var(wcs_userid) 0
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] OnChange (END)
	}
}

block speed
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 1 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		es wcs_setfx speed event_var(userid) + server_var(wcs_speed) 0
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Ice skate - You recived bonus speed
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 1 (END)
	}
}

block freeze
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 2 (START)
	}
	if (server_var(wcs_dice2) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcsgroup get is_frozen wcs_tmp4 event_var(userid)
				if (server_var(wcs_tmp4) == 0) do
				{
					es wcsgroup set is_frozen event_var(userid) 1
					es_delayed 1.5 wcsgroup set is_frozen event_var(userid) 0
					es wcs_fade event_var(userid) 0 0 200 40 1 1
					es wcs_setfx freeze event_var(userid) = 1 1.5
					es_tell event_var(userid) #multi #green [WCS]#lightgreen Lips of ice - You froze an enemy for 1.5 seconds
					es_tell event_var(attacker) #multi #green [WCS]#lightgreen Lips of ice - You've been frozen for 1.5 seconds
					es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
					es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 200 1 120 150 0 0 50 255 100 5
					es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 30 50 11 120 150 0 0 50 255 100 5
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 2 (END)
	}
}

block damage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= 12) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_centertell event_var(attacker) Frost breath dealt server_var(wcs_dmg) more damage
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
				es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(attacker)
				es_xmath wcs_z1 + 55
				es_xmath wcs_z2 + 55
				es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
				es_createvectorstring vector2 server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2)
				es est_effect_06 #a 0 sprites/lgtning.vmt server_var(vector1) server_var(vector2) 1 1 1 1 1 0 0 50 255 255 30
				es est_effect_06 #a 0 sprites/laserbeam.vmt server_var(vector1) server_var(vector2) 1 1 1 1 1 0 0 50 255 255 30
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 3 (END)
	}
}

block health_evade
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 4 (START)
	}
	if (event_var(es_userdead) = 0) do
	{
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_playsound event_var(userid) ambient/wind/wind_snippet2.wav .7
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es_playsound event_var(userid) ambient\wind\css15_wind_01.wav .8
		}
		es wcsgroup set evasion event_var(userid) 1
		es wcsgroup set evasion_chance event_var(userid) server_var(wcs_chance)
	    es wcsgroup get evasion_count wcs_tmp1 event_var(userid)
	    es_tell event_var(userid) #multi #green [WCS]#lightgreen Ice block - You evaded server_var(wcs_tmp1) attacks last round
		es_tell event_var(userid) #multi #green [WCS]#lightgreen Ice block - You received 25 health and server_var(wcs_chance) chance to evade
		es wcs_setfx health event_var(userid) + 25 0
		es_delayed .5 wcs_color event_var(userid) 0 0 255 255 1
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
		es_xmath wcs_z1 + 10
		es est_effect 10 #a 0 sprites/tp_beam001.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 250 3 10 1 1 0 0 255 200 3
		es est_effect 10 #a 0 sprites/tp_beam001.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 100 1 7 1 1 0 0 255 200 3
		es est_effect 10 #a 0 sprites/tp_beam001.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 150 1 7 1 1 0 0 255 200 3
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 4 (END)
	}
}

block team_health
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 5 (START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_userid) ambient/wind/wind_snippet2.wav .7
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_userid) ambient\wind\css15_wind_01.wav .8
		}
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen They shall not freeze - You feel a gentle breeze
		es wcs_setfx health server_var(wcs_userid) + server_var(wcs_addhealth) 0
		es wcs_fade server_var(wcs_userid) 0 0 255 40 1 1
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
		es_xmath wcs_z1 + 20
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 200 1 150 150 0 0 50 255 100 5
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 200 1 50 150 0 255 255 255 100 5
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) == 3) do
		{
			es_xset wcs_targetteam "#ct,#alive"
		}
		if (server_var(wcs_team) == 2) do
		{
			es_xset wcs_targetteam "#t,#alive"
		}
		es wcs_foreach player wcs_targetid server_var(wcs_targetteam) "es_xdoblock wcs/tools/manifest/frozen_breath/team_health_exe"
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 5 (END)
	}
}

block team_health_exe
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 5 (EXECUTED START)
	}
	es_xset wcs_pcount 0
	es_getlivingplayercount wcs_pcount server_var(wcs_team)
	if (server_var(wcs_pcount) > 0) do
	{
		if ("Counter-Strike: Source" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_targetid) ambient/wind/wind_snippet2.wav .7
		}
		if ("Counter-Strike: Global Offensive" in server_var(wcs_gamecheck)) do
		{
			es_playsound server_var(wcs_targetid) ambient\wind\css15_wind_01.wav .8
		}
		es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen They shall not freeze - You feel a gentle breeze
		es wcs_setfx health server_var(wcs_targetid) + server_var(wcs_addhealth) 0
		es wcs_fade server_var(wcs_targetid) 0 0 255 40 1 1
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_targetid)
		es_xmath wcs_z1 + 20
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 200 1 150 150 0 0 50 255 100 5
		es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 200 1 50 150 0 255 255 255 100 5
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Frozen Breath - [Block] Skill: 5 (EXECUTED END)
	}
}