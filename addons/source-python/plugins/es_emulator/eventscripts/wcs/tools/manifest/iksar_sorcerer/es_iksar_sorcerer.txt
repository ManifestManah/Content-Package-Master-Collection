/////////////////////////////
//Various Authors Race Pack//
//Version 1.1.0A           //
/////////////////////////////

block damage
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 1 (START)
	}
	if (server_var(wcs_dice) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es_xrand wcs_dmg 8 16
				es damage event_var(userid) server_var(wcs_dmg) 1 event_var(attacker)
				es wcs_centertell event_var(attacker) You've poisoned an enemy!
				es wcs_centertell event_var(userid) You've been poisoned by an enemy!
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 1 (END)
	}
}

block spirit
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 2 (START)
	}
	es wcs_setfx speed event_var(userid) = server_var(wcs_speed) 0
	es_xmath wcs_speed * 100
	es_xmath wcs_speed - 100
	es_delayed .2 wcs_setfx health event_var(userid) + server_var(wcs_addhealth) 0
	es_tell event_var(userid) #multi #green [WCS]#lightgreen Flaming spirit - Granted you server_var(wcs_addhealth) more health and server_var(wcs_speed)% extra speed
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es_xmath wcs_z1 + 40
	es est_effect 10 #a 0 sprites/plasma1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 45 2 60 100 4 155 0 0 155 2
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 2 (END)
	}
}

block distort
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcsgroup get is_already_colored wcs_tmp6 event_var(userid)
				if (server_var(wcs_tmp6) == 0) do
				{
					es wcsgroup set is_already_colored event_var(userid) 1
					es_delayed 3 es wcsgroup set is_already_colored event_var(userid) 0
					es wcs_getcolors event_var(userid) wcs_red wcs_green wcs_blue wcs_alpha
					es_delayed .1 wcs_color event_var(userid) 255 255 0 255 1
					es_delayed 3.1 wcs_color event_var(userid) server_var(wcs_red) server_var(wcs_green) server_var(wcs_blue) server_var(wcs_alpha) 1
					es wcs_centertell event_var(userid) An Iksar Sorcerer turned you yellow for 3 seconds!
					es wcs_centertell event_var(attacker) You turned event_var(es_username) yellow for 3 seconds!
					es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
					es_xmath wcs_z1 + 15
					es est_effect 10 #a 0 sprites/plasmabeam.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 25 45 2 60 100 0 55 55 55 155 2
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 3 (END)
	}
}

block time
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 4 (START)
	}
	es_xset wcs_execute_ultimate 0
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_targetteam "#alive"
		es_getlivingplayercount wcs_pcount
		es_set wcs_ffa_userid server_var(wcs_userid)
	}
	ifx false(wcs_ffa_enabled) do
	{
		es_getplayerteam wcs_team server_var(wcs_userid)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_targetteam "#ct,#alive"
			es_getlivingplayercount wcs_pcount 3
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_targetteam "#t,#alive"
			es_getlivingplayercount wcs_pcount 2
		}
	}
	if (server_var(wcs_targetteam) != 0) do
	{	
		if (server_var(wcs_pcount) > 0) do
		{
			es_xset wcs_counter 0
			es_xset wcs_initial_players_struck 0
			es_xset wcs_count_players_ultimate_immunity 0
			es_xset wcs_count_players_actually_affected 0
			es wcs_foreach player wcs_targetid server_var(wcs_targetteam) "es_xdoblock wcs/tools/manifest/iksar_sorcerer/ultimate_checks"
			if (server_var(wcs_execute_ultimate) == 1) do
			{
				es wcs_centertell server_var(wcs_userid) Time Rift slowed down server_var(wcs_count_players_actually_affected) enemies for 2 seconds
				es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Time rift - Slowed all enemies down to 75% movement speed for 2 seconds
				es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
				es est_effect 10 #a 0 sprites/plasma1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 350 2 90 200 0 155 155 155 155 2
			}
		}
		else do
		{
			es_xset wcs_ultinotexec 1
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, there are no living enemies!
		}
		if (server_var(wcs_counter) = 0) do
		{
			es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, no nearby enemies!
			es_xset wcs_ultinotexec 1
		}
	}
	else do
	{
		es_xset wcs_ultinotexec 1
		es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Could not target an enemy team
	}
	if (server_var(wcs_ultinotexec) = 1) then es wcs_cancelulti server_var(wcs_userid)
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 4 (END)
	}
}

block ultimate_checks 
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 4 (CHECKS START)
	}
	ifx true(wcs_ffa_enabled) do
	{
		es_xset wcs_execute_ultimate_ffa 1
		if (server_var(wcs_targetid) != server_var(wcs_ffa_userid)) do
		{
			es_xset wcs_execute_ultimate_ffa 0
		}
	}
	ifx false(wcs_execute_ultimate_ffa) do
	{
		es_exists ex userid server_var(wcs_targetid)
		if (server_var(ex) = 1) do
		{
			es getplayerinfo wcs_alive server_var(wcs_targetid) isdead
			if (server_var(wcs_alive) = 0) do
			{
				es_xmath wcs_counter + 1
				if (server_var(wcs_counter) > 0) do
				{
					if (server_var(wcs_counter) <= 20) do
					{
						es_set wcs_initial_players_struck server_var(wcs_counter)
						es wcsgroup get ulti_immunity wcs_tmp server_var(wcs_targetid)
						if (server_var(wcs_tmp) != 1) do
						{
							es_xset wcs_ultinotexec 0
							es_xset wcs_execute_ultimate 1
							es_xmath wcs_count_players_actually_affected + 1
							es_xdoblock wcs/tools/manifest/iksar_sorcerer/ultimate_final
						}
						if (server_var(wcs_tmp) == 1) do
						{
							es_xmath wcs_count_players_ultimate_immunity + 1
							if (server_var(wcs_count_players_ultimate_immunity) != server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Ultimate immunity - You blocked an enemy's ultimate
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - server_var(wcs_count_players_ultimate_immunity) enemies were immune to your ultimate!
							}
							if (server_var(wcs_count_players_ultimate_immunity) == server_var(wcs_initial_players_struck)) do
							{
								es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, all nearby enemy targets were immune!
								es_xset wcs_ultinotexec 1
							}
						}
					}
					else do
					{
						es_tell server_var(wcs_userid) #multi #green [WCS]#lightgreen Ultimate - Failed, you can't hit that many targets at once!
						es_xset wcs_ultinotexec 1
					}
				}
			}
		}	
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 4 (CHECKS END)
	}
}

block ultimate_final
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 4 (EXECUTED START)
	}
	es wcsgroup get is_slowed wcs_tmp3 server_var(wcs_targetid)
	if (server_var(wcs_tmp3) == 0) do
	{
		es wcsgroup set is_slowed server_var(wcs_targetid) 1
		es_delayed 2 wcsgroup set is_slowed server_var(wcs_targetid) 0
		es wcs_setfx speed server_var(wcs_targetid) = .75 2
		es_tell server_var(wcs_targetid) #multi #green [WCS]#lightgreen Time rift - An Iksar Sorcerer slowed down the time for 2 seconds
		es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_targetid)
		es est_effect 10 #a 0 sprites/plasma1.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 50 350 2 90 200 0 155 155 155 155 2
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log  [Race]: Iksar Sorcerer - [Block] Skill: 4 (EXECUTED END)
	}
}